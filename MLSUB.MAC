	.TITLE MLSUB-SUBROUTINES FOR MILLIPEDE
	.NLIST
	.INCLUD MLDEF
	.LIST
	.CSECT MLSUB


	.SBTTL	BONUS - DISPLAY "BONUS LIFE EVERY XXXX" MESSAGE
BONUS:	JSR BONUS1		;GET BONUS INCREMENT
	STA BONUSL
	STA BONUSL+1		;IN CASE PLAYR=1 BELOW
	LDA Y,BONUSV+1
	STA BONUSM
	STA BONUSM+1
	CPY I,06
	BEQ 90$
	LDA I,6
BONUS0	=.
	JSR MESS		;DISPLAY "BONUS LIFE EVERY"
	LDY PLAYR
	LDA Y,BONUSM
	SEC			;ZERO SUPPRESSION
	JSR DIGIT2		;DISPLAY DIGITS
	LDY PLAYR
	LDA Y,BONUSL
	JSR DIGIT2		;DISPLAY 2 DIGITS
	CLC
	LDA I,0
	JMP DIGIT2		;2 TRAILING ZEROS

BONUS1	=.
	LDA OPTNS1
	AND I,30		;D4-D5=BONUS LEVEL OPTIONS
	LSR
	LSR
	LSR
	TAY
	LDA Y,BONUSV		;GET BONUS VALUE INCREMENT
90$:	RTS


BONUS2	=.
	JSR BONUS1		;GET BONUS LEVEL SELECTED
	CPY I,06
	BEQ 90$			;NO BONUS ALLOWED
	LDA I,11.
	JMP BONUS0		;DISPLAY "NEXT BONUS AT XXXXXX"


	.SBTTL	CHKEND-CHECK FOR DELAYS AND END OF GAME
;
CHKEND:	LDA DELAY
	BEQ 1$			;IF NO DELAY
	LDA MEM+1		;WAIT UNTIL MUSHROOMS RESTORED
	ORA PEXPLD		;WAIT UNTIL PLAYER STOPS EXPLODING
	ORA BEETLS
	BNE 1$			;DON'T BRING IN CENTIPEDE WHILE BEETLES ARE PRESENT
	DEC DELAY
	BNE 1$			;DON'T DO ANYTHING YET
	LDA SWTCHP
	BEQ 40$			;IF WE ARE NOT SWITCHING PLAYERS
	LDA I,80
	JSR MESS		;CLEAR "PLAYER" MESSAGE
	LDA I,0
	TAY
	STA NY,TEMP4		;CLEAR PLAYER NUMBER
	STA SWTCHP		;CLEAR FLAG
	JSR DDTS2		;RESET DDT BOMBS
	JMP INIT1		;RESET GUN AND SHOT

40$:	LDA PLAYP
	EOR I,0A
	BNE 50$			;IF GUN IS NOT ALIVE
	LDA I,0
	LDX I,NCENT-1
42$:	ORA X,MOBJC
	DEX
	BPL 42$			;BE SURE ALL ENTRIES ARE BLANK
	TAX
	BEQ 45$			;IF NO CREATURES
	INC DELAY		;WAIT FOR SPIDERS, INCHWORM OR SCORES TO DISAPPEAR
1$:	RTS

45$:	JMP 90$			;WE ARE ON NEXT WAVE

50$:	LDX I,13.
	JSR SPDOFF		;GET BEE AND SPIDER OFF THE SCREEN
	LDX I,12.
	JSR BEEOFF
	LDA MODE
	BPL 55$			;IF NOT IN ATTRACT
	JSR INIT1		;INITIALIZE PLAYER
	LDA I,0
	LDX I,NCENT-1
51$:	STA X,MOBJH
	STA X,MOBJC
	DEX
	BPL 51$			;CLEAR OUT ALL OBJECTS
	JMP CENTPC		;START THE CENTIPEDE

55$:	LDA LIVES
	ORA LIVES+1
	BNE 70$			;IF GAME NOT OVER
	DEC MODE		;MODE=-1 FOR ATTRACT
	JSR UPDATE		;UPDATE HIGH SCORE TABLE
	JSR SELEC2		;SET UP PARAMETERS FOR STARTING SELECT MODE
	BIT CKIND
	BMI 58$			;IF IN COCKTAIL MODE
	BVC 56$			;NOT IN COCKTAIL CABINET
	LDA UPDFLG+1
	BMI 60$			;IF PLAYER 2 WILL NOT ENTER INITIALS
	LDA I,1
	STA PLAYR
	JSR CKSET		;SET UP COCKTAIL FLIP
54$:	JSR SWAP		;SWAP TO PLAYER 2 PLAYFIELD
	JMP 60$

56$:	LDX PLAYR
	BEQ 57$			;IF PLAYER 1 WAS LAST ONE UP
	LDA UPDFLG+1
	BPL 60$			;PLAYER 2 WILL BE ASKED FOR HIS INITIALS
	DEC PLAYR		;SET TO PLAYER 1
	BEQ 59$			;ALWAYS

57$:	LDA UPDFLG+1
	BMI 60$			;PLAYER 2 WAS NOT ON HIGH SCORE TABLE
	INC PLAYR		;SET TO PLAYER 2
	BNE 54$			;ALWAYS

58$:	LDA UPDFLG+1
	BPL 60$			;IF PLAYER 2 WILL BE ASKED FOR HIS INITIALS
	LDA I,0
	STA PLAYR
	JSR CKCLR		;CLEAR COCKTAIL INDICATORS
59$:	JSR SWAP
60$:	JSR INIT1		;REWRITE PLAYER IN CASE WE FLIPPED THE SCREEN
	JSR CENTPC		;REWRITE "DEAD" FOR PLAYER 1
	LDX NPLAYR
	LDA I,0FF
	STA X,STRTL1-1		;TURN OFF START LED
	JSR CKSUM		;CHECKSUM EA ROM BUFFER.
	LDA I,EACKSM-EAROM
	STA ROMA		;STARTING ADDRESS
	LDA I,0
	STA ROMC		;COMPARE/WRITE EA ROM
	JSR SCORES		;DISPLAY HIGH SCORE TABLE
	LDA UPDFLG
	AND UPDFLG+1
	BPL 68$			;IF WE ARE ENTERING INITIALS
	LDA I,4
	JSR MESS		;DISPLAY "GAME OVER"
68$:	RTS

70$:	LDX NPLAYR
	DEX
	BNE 71$			;2 PLAYERS
69$:	JSR INIT1		;INITIALIZE PLAYER.
	JMP 80$			;IF 1 PLAYER GAME CONTINUE

71$:	LDX PLAYR
	LDA X,LIVES  
	BNE 73$			;IF CURRENT PLAYER STILL HAS A LIFE
	LDA GOVER
	BNE 72$			;TIME TO REMOVE "GAME OVER" MESSAGE
	INC GOVER
	LDA I,80
	STA DELAY		;DELAY AGAIN
	LDA I,0
	STA PLAYP		;REMOVE GUN AND SHOT
	STA PLAYH
	STA SHOTH
	LDA I,4
	JSR MESS		;PUT OUT GAME OVER...
	LDA I,0
	JSR MESS		;PLAYER MESSAGE
	LDA PLAYR
	CLC
	ADC I,21
	JSR CHAR		;PLAYER NUMBER
	RTS

72$:	DEC GOVER
73$:	LDA PLAYR
	EOR I,01		;SWITCH PLAYER NUMBER
	TAX
	LDA X,LIVES  
	BEQ 69$			;NO LIVES FOR OTHER PLAYER
	STX PLAYR		;SWITCH PLAYERS
	LDA CKIND
	BEQ 738$		;NOT COCKTAIL
	BMI 738$		;COCKTAIL NOW
	JSR CKSET
	JMP 739$

738$:	JSR CKCLR
739$:	LDA X,COUNT3  
	STA COUNT1		;RESET NEW HEAD COUNTER
	JSR SWAP		;SWAP PLAYERS' SCREENS
	LDX PLAYR
	BEQ 75$			;CONTINUE IF PLAYER 1
	LDA X,LIVES  
	CMP NLIVES
	BNE 75$			;CONTINUE IF NOT PLAYER 2'S FIRST CHANCE
	LDA SCORE2+1		;BONUS LEVEL MUST BE AT LEAST 10,000
	BNE 75$			;IF PLAYER TWO IS ON BONUS LIFE
	LDA I,0
	BIT OPTNS1
	BMI 74$			;IF SELECT MODE IS DISABLED
	LDA LSCORE+1
	BEQ 74$			;NO OTHER SCORE TO START AT
	LDA I,1
74$:	STA MODE
	LDA I,NCENT
	STA X,DEAD  		;DO THIS SO COLOR GETS INITIALIZED CORRECTLY
	JSR INITSC		;INITIALIZE SCREEN FOR PLAYER 2
	JSR SELEC1		;INITIALIZE FOR SELECTING STARTING SCORE
75$:	LDA I,0A0
	STA DELAY		;DELAY FOR NEXT PLAYER
	LDA I,0
	JSR MESS		;"PLAYER" MESSAGE
	LDA PLAYR
	CLC
	ADC I,21
	STA SWTCHP		;FLAG FOR SWITCHING PLAYERS
	JSR CHAR		;DISPLAY 1 OR 2
	LDA MODE		;REMOVE GUN SO "PLAYER" MESSAGE WILL BE DELETED
	BNE 80$			;IF IN SELECT MODE DO NOT REMOVE PLAYER
	STA PLAYP
	STA SHOTH
	STA PLAYH
80$:	LDX PLAYR
	DEC X,LIVES  		;START NEXT LIFE
	JSR DLIVES		;DISLAY LIVES
90$:	JMP CENTPC		;GET NEW CENTIPEDE


CKCLR:	LDA I,0
	STA CKF8
	STA CKFF
	STA CKFE
	LDA COCKSW
	AND I,20		;GET COCKTAIL SWITCH
	EOR I,20		;1=COCKTAIL NOW
	ASL			;SET OR CLEAR COCKTAIL FLAG
CKCLR1:	STA CTRLSL		;BACK TO PLAYER 1 CONTROLS
	STA VIDROT		;ROTATE VIDEO PICTURES TOO
	STA CKIND		;SET OR CLEAR COCKTAIL AND PLAYER FLAG
	LDA TBALL		;READ IN PRESET VALUE FROM COUNTER
	STA OLDTB
	LDA TBALL+1
	STA OLDTB+1
	RTS


CKSET:	LDA I,0F8
	STA CKF8
	LDA I,0FF
	STA CKFF
	LDA I,0FE
	STA CKFE
	LDA I,0C0
	BNE CKCLR1		;ALWAYS


	.SBTTL	CHKST-CHECK FOR START IF GAME
;CHKST-CHECK FOR START OF GAME
;
CHKST:	LDA $CMODE		;SET COIN MODE
	AND I,03
	STA TEMP2
	BNE 5$			;IF NOT FREE PLAY
	LDA I,2
	STA $$CRDT		;GIVE 2 CREDITS IN FREE PLAY
5$:	LDA OPTNS1
	AND I,0C
	LSR
	LSR
	ADC I,2
	STA NLIVES		;2 TO 5 LIVES TO START WITH
	LDA MODE
	BMI 7$			;NOT IN GAME PLAY
50$:	RTS

7$:	LDA TEMP2
	BEQ 8$			;NO FREE PLAY MESSAGE
	JSR MESS		;GAME COST MESSAGE
8$:	LDA FRAME
	AND I,8
	ASL
	ASL
	ASL
	ASL
	STA TEMP2
	LDA $$CRDT
	ORA $CNCT
	BEQ 85$			;NO CREDITS IN YET
	LDX TCMFLG
	BEQ 10$			;NOT TWO CREDIT MINIMUM
	CMP I,2
	BCC 95$			;FLASH TCM WHEN CREDITS < 2
	LDA I,0
	STA TCMFLG		;CLEAR FLAG WHEN CREDITS > 1
	LDA I,8A		;ERASE "2 CREDIT MINIMUMS" MESSAGE
	BNE 97$			;ALWAYS

85$:	LDA OPTNS2
	AND I,40
	STA TCMFLG		;SET/CLEAR 2 CREDIT MINIMUM FLAG
	BEQ 87$			;NO 2 CREDIT MINIMUM
	LDA I,0A
	JSR MESS		;EITHER CLEAR OR WRITE MESSAGE
87$:	LDX I,-1
	STX STRTL1		;TURN OFF START LED'S
	STX STRTL2
92$:	RTS


95$:	LDA I,0A
	ORA TEMP2
97$:	JSR MESS		;FLASH TCM
10$:	LDA I,9
	JSR MESS		;"CREDITS"
	LDA $$CRDT
	CMP I,10.
	BCC 15$			;9 OR LESS CREDITS
	LDA I,21
	JSR CHAR		;10 TO 16 CREDITS
	LDA $$CRDT
	SEC
	SBC I,10.
15$:	ORA I,20		;1 THRU 9 ARE 21 TO 29
	JSR CHAR
	LDA $CNCT
	BEQ 18$			;IF NO 1/2 CREDIT
	LDA I,1E
18$:	JSR CHAR		;PUT UP 1/2 OR BLANK
	LDX $$CRDT
	BEQ 92$			;IF NO CREDITS
	LDA TCMFLG
	BNE 92$			;NOT ENOUGH CREDIT IN TCM
	LDA TEMP2
	STA STRTL1		;FLSH 1 PLAYER START LED
	CPX I,02
	BCC 20$			;ONLY ENOUGH CREDIT FOR ONE
	STA STRTL2		;FLASH 2 PLAYER START LED
	LDX I,02
	LDA START2
	AND I,20
	BNE 20$			;IF 2 PLAYER GAME NOT STARTED
	LDA NLIVES
	STA LIVES+1
	DEC $$CRDT		;DELETE CREDIT
	BPL 25$			;ALWAYS.

20$:	LDA START1
	LDX I,1
	AND I,20
	BNE 92$			;IF 1 PLAYER GAME NOT STARTED
25$:	DEC $$CRDT
	LDA I,-1
	STA TBEN		;BE SURE TRACKBALLS CAN BE READ
	STA STRTL1
	STA STRTL2		;TURN OFF START LED'S
	LDA I,0
	STA HILITE		;CLEAR ATTRACT HILITE FLAG
	STA NOCENT		;CLEAR BOMB MODE
	STA SLOW		;STOP SLOW DOWN MODE
	STA GOVER		;CLEAR ANY GAME OVER FLAG
	STA DELAY		;CLEAR ANY DELAY
	STA PEXPLD		;CLEAR ANY PLAYER EXPLOSION
	STA CDONE		;STOP GROWTH/DEATH OF MUSHROOMS
	STA SCROLC		;CLEAR SCROLL COUNT
	STA GTIME
	STA GTIME+1		;CLEAR GAME TIMER
	STA EXTRAL		;CLEAR COUNT OF BONUS LIVES
	STA EXTRAL+1
	STA SDBNCE		;ALLOW FOR DEBOUNCE IN "GETINT"
	STA $BC
	STA $BCCNT		;CLEAR BONUS ADDER FLAGS
	STX NPLAYR		;NUMBER OF PLAYERS
	STA X,STRTL1-1		;TURN ON APPROPRIATE START LED
	LDX NLIVES
	DEX
	STX LIVES		;NUMBER OF LIVES-1 (WE ARE PLAYING WITH ONE)
	LDA I,0
	BIT OPTNS1
	BMI 30$			;IF SELECT MODE IS DISABLED
	LDA LSCORE+1
	BEQ 30$			;NO OTHER SCORE TO START AT
	LDA I,1
30$:	STA MODE		;SELECT STARTING SCORE MODE
	JSR COPYHS		;COPY HIGH SCORES TO EA ROM BUFFER
	JSR BONUS1		;GET BONUS LEVEL VALUES
	STA BONUSL
	STA BONUSL+1
	LDA Y,BONUSV+1		;SET INITIAL BONUS VALUES
	STA BONUSM
	STA BONUSM+1
	JSR CKCLR		;JUST IN CASE WE WERE ASKING FOR PLAYER 2'S INITIALS
	JSR INIT		;INITIALIZE EVERYTHING
	JSR SELEC1		;INITIALIZE SELECT MODE MESSAGES.
	JMP DLIVES		;DISPLAY LIVES



	.SBTTL	COPYHS-COPY HIGH SCORES TO EA ROM BUFFER
COPYHS:	LDA I,-1
	STA UPDFLG
	STA UPDFLG+1		;STOP GETTING ANY INITIALS FROM PLAYER
	LDX I,8
10$:	LDA X,HSCORE
	STA X,EAROM		;SAVE FIRST 3 SCORES
	LDA X,INITL
	STA X,EAROM+9		;SAVE FIRST 3 PLAYERS INITIALS
	DEX
	BPL 10$
	JMP CKSUM		;CHECK EAROM


	.SBTTL	DDTS - ADD DDT BOMBS
DDTS:	JSR DDTS1		;SET UP PARAMETERS
	LDY I,NDDT*2-1
	BIT CKIND
	BPL 14$			;IF NOT COCKTAIL
12$:	LDA Y,DDTST+8		;SET DDT ADDRESSES FOR COCKTAIL
	STA X,DDTADD
	STA Y,TEMP1
	DEX
	DEY
	BPL 12$
	BMI DDTS2		;ALWAYS

14$:	LDA MODE 
	EOR I,0FF
	BNE 15$			;IF NOT SPECIAL ATRACT MODE
	LDY I,17		;USE SPECIAL DDT PLACEMENTS
15$:	LDA Y,DDTST
	STA X,DDTADD		;SET DDT ADDRESSES
	STA Y,TEMP1
	DEX
	DEY
	BPL 15$
DDTS2:	JSR DDTS1		;SET UP PARAMETERS
10$:	LDA X,DDTADD
	BEQ 20$			;NO ENTRY
	CMP I,PLYFLD+400/100
	BCS 20$			;IF EXPLODING
	STA TEMP1+1
	LDA X,DDTADD-1
	STA TEMP1
	LDY I,20
	BIT CKIND
	BPL 15$			;IF NOT COCKTAIL
	LDY I,0			;FLIP PICTURE ORDER
15$:	LDA NY,TEMP1
	AND I,7F
	BEQ 16$			;IF A BLANK
	CMP I,ROCK		;WE CAN OVERWRITE A MUSHROOM IN ATTRACT
	BCC 18$			;IF SOMETHING IS THERE
16$:	LDA I,DDT+1
	STA NY,TEMP1
18$:	TYA
	EOR I,20
	TAY
	LDA NY,TEMP1
	AND I,7F
	BEQ 19$			;IF A BLANK
	CMP I,ROCK		;WE CAN OVERWRITE A MUSHROOM IN ATTRACT
	BCC 20$			;IF SOMETHING IS THERE
19$:	LDA I,DDT
	STA NY,TEMP1		;WRITE OUT PICTURE STAMPS
20$:	DEX
	DEX
	DEC TEMP2
	BPL 10$
	RTS

;
;EXIT	(X)=INDEX FOR "DDTADD" TABLE
;	(TEMP2)=NUMBER OF DDT BOMBS - 1
DDTS1:	LDA PLAYR
	ASL
	ASL
	ASL
	ORA I,NDDT*2-1
	TAX
	LDA I,NDDT-1
	STA TEMP2
	RTS

DDTST:	.WORD 10CD		;STARTING ADDRESSES OF DDT BOMBS
	.WORD 1119
	.WORD 1233
	.WORD 12F7
	.WORD 12F2		;COCKTAIL PLAYER 2 POSITIONS
	.WORD 12A6
	.WORD 118C
	.WORD 10C8
	.WORD 10A3
	.WORD 1084
	.WORD 1086
	.WORD 0


	.SBTTL	DLIVES-DISPLAY LIVES
;
DLIVES:	LDA I,6
	STA TEMP1		;MAX NUMBER OF LIVES
	LDA I,PLYFLD+0DF/100
	LDY I,PLYFLD+0DF&0FF
	BIT CKIND
	BPL 5$			;IF UPRIGHT
	LDA I,PLYFLD+2E0/100
	LDY I,PLYFLD+2E0&0FF
5$:	STY TEMP4
	STA TEMP4+1
	LDX LIVES
10$:	LDA I,1F		;PICTURE OF SHIP
	DEX
	BPL 15$
	LDA I,0			;NO MORE SHIPS
15$:	JSR CHAR		;PUT OUT SHIP OR BLANK
	DEC TEMP1
	BNE 10$
	LDA I,PLYFLD+25F/100
	LDY I,PLYFLD+25F&0FF
	BIT CKIND
	BPL 18$			;IF UPRIGHT
	LDA I,PLYFLD+160/100
	LDY I,PLYFLD+160&0FF
18$:	STY TEMP4
	STA TEMP4+1
	LDA I,6
	STA TEMP1
	SEC
	SBC LIVES+1
	TAX			;NUMBER OF BLANKS TO DISLAY
20$:	LDA I,0
	DEX
	BPL 25$			;PUT OUT BLANK
	LDA I,1F
25$:	JSR CHAR		;DISPLAY SHIP OR BLANK.
	DEC TEMP1
	BNE 20$
	JMP BONUS2		;DISPLAY NEXT BONUS LEVEL.
	.PAGE
	.SBTTL	GETINT-GET PLAYERS INITIALS FOR HIGH CORE
;GETINT-GET PLAYERS INITIALS FOR HIGH SCORE
;
;EXIT (CC)=POSITIVE FLAG SET IF WE ARE ENTERING INITIALS
GETINT:	LDA UPDFLG
	AND UPDFLG+1
	BPL 10$			;GET PLAYERS INITIALS
	RTS

10$:	LDA NPLAYR
	LSR
	BEQ 25$			;IF LAST GAME WAS 1 PLAYER GAME
	LDA I,0
	JSR MESS		;DISPLAY "PLAYER"
	LDA PLAYR
	CLC
	ADC I,21		;DISPLAY CODE FOR 1 OR 2
	JSR CHAR		;DISPLAY PLAYER NUMBER
25$:	LDA I,8
	JSR MESS		;DISPLAY "GREAT SCORE"
	LDA I,5
	JSR MESS		;DISPLAY "ENTER YOUR INITIALS"
	LDA I,PLYFLD+185&0FF
	LDY I,PLYFLD+185/100
	BIT CKIND
	BPL 28$			;IF UPRIGHT
	LDA I,PLYFLD+23A&0FF
	LDY I,PLYFLD+23A/100
28$:	STA TEMP4
	STY TEMP4+1
	LDX PLAYR
	LDY X,UPDFLG  
	STY TEMP2
	TYA
	CLC
	ADC UPDINT
	STA TEMP2+1		;INDEX FOR THE INITIAL WE ARE WORKING ON
	SEC
	ROR HILITE		;SET HIGHLITE FLAG
	JSR INITAL		;DISPLAY INITIAL
	LDY TEMP2
	INY
	JSR INITAL		;DISPLAY INITIAL
	LDY TEMP2
	INY
	INY
	JSR INITAL		;DISPLAY THIRD INITIAL
	ASL HILITE		;CLEAR FLAG
	LDA FIRE1
	BIT CKIND
	BPL 30$			;NOT COCKTAIL
	LDA FIRE2		;FIRE FOR PLAYER 2 IN COCKTAIL
30$:	AND I,10		;D4=FIRE SWITCH
	CMP I,10
	ROL SDBNCE
	LDA SDBNCE
	AND I,1F
	CMP I,18
	BNE 60$			;2 FRAMES OFF 3 FRAMES ON
	LDX TEMP2+1
	LDA X,INITL
	CMP I,BCKSPC
	BNE 40$			;IF NOT A BACKSPACE
	LDA UPDINT
	BEQ 60$			;NO CHANGE IF ON FIRST INITIAL
	DEC UPDINT		;BACKUP AN INITIAL
	DEC TEMP2+1
	LDA I,0
	BEQ 58$			;CLEAR CURRENT INITIAL

40$:	INC UPDINT
	LDA UPDINT
	CMP I,03
	BCC 55$			;IF WE ARE NOT DONE
52$:	LDX PLAYR
	LDA I,-1
	STA X,UPDFLG  		;CLEAR UPDATING FLAG
	TXA
	BEQ 53$			;IF PLAYER 1 ALREADY
	LDA I,0
	STA PLAYR		;RESET TO PLAYER 0
	JSR CKCLR		;CLEAR COCKTAIL INDICATORS
	JSR SWAP		;SWITCH TO PLAYER 1'S SCREEN
	JSR INIT1		;RESET PLAYERS GUN
	JSR CENTPC		;SET CENTIPEDE FOR PLAYER 1
	JSR SCORES		;WRITE HIGH SCORE TABLE
53$:	LDA UPDFLG
	AND UPDFLG+1
	BMI 61$			;REMOVE MESSAGES
	LDX I,0
	STX UPDINT		;START NEXT PLAYER
	RTS

55$:	INC TEMP2+1
	LDX TEMP2+1
	LDA I,0F8		;ABOUT 30 SECONDS
	STA FRAME+1		;RESET TIMEOUT
	LDA I,1
58$:	STA X,INITL		;SET INITIAL TO A
	JSR SCORES		;REWRITE HIGH SCORE TABLE
60$:	LDA FRAME+1
	BEQ 52$			;TIME OUT - BACK TO ATTRACT
	BNE 65$

61$:	LDA I,88
	JSR MESS		;REMOVE "GREAT SCORE"
	LDA I,85
	JSR MESS		;REMOVE "ENTER INITIALS"
	LDA I,80
	STA PLYFLD+185		;REMOVE INITIALS
	STA PLYFLD+1A5
	STA PLYFLD+1C5
	JSR COPYHS		;COPY HIGH SCORES TO EAROM BUFFER
	JSR SCORES		;DISPLAY HIGH SCORE TABLE
	LDX NPLAYR
	LDA I,0
	DEX
	BEQ 64$			;IF ONLY 1 PLAYER GAME
	LDA I,80
	JSR MESS		;CLEAR "PLAYER" MESSAGE
	LDA I,0
	TAY
	STA NY,TEMP4		;CLEAR PLAYER NUMBER
64$:	STA UPDINT		;SET UP FOR NEXT PLAYER
	LDA I,4
	JSR MESS		;DISPLAY "GAME OVER"
	JMP DDTS2		;RESTORE DDT BOMBS

65$:	LDA FRAME
	AND I,07
	BNE 90$			;EVERY 8TH FRAME
	LDX I,-1
	LDA I,0
	LDY TB			;GET HORIZONTAL TRACKBALL READINGS
	STA TB			;CLEAR FOR NEXT FRAME
	BPL 70$			;IF GOING ONE WAY
	LDX I,1
	TYA
	JSR COMP		;COMPLEMENT A
	TAY
70$:	CPY I,04
	BCC 90$			;IF NO CHANGE
	TXA
	EOR CKFE
	LDX TEMP2+1
	CLC
	ADC X,INITL		;CHANGE INITIAL
	BMI 78$			;BEFORE A BLANK MUST BE BACKSPACE
	CMP I,BCKSPC+1
	BCC 85$			;IF LESS THAN BACKSPACE+1
	LDA I,0			;BEFORE A COMES A BLANK
	BEQ 85$

78$:	LDA I,BCKSPC		;MAKE IT A BACKSPACE
85$:	STA X,INITL
88$:	JSR SCORES		;REWRITE HIGH SCORE TABLE
90$:	LDA I,0			;MUST BE POSITIVE ON RETURN
	RTS


	.SBTTL	MUSHDC - DECREMENT COUNT OF MUSHROOMS
;
;ENTRY	(OBST,OBST+1)=PLAYFIELD ADDRESS
;USES	A,X
MUSHDC:	LDA OBST
MUSHD1:	AND I,1F
	LDX PLAYR
	BIT CKIND
	BPL 5$			;NOT TIME FOR COCKTAIL
	CMP I,14
	BCS 7$			;IF ON UPPER HALF OF SCREEN FOR COCKTAIL
	CMP I,0C
	BCC 6$			;ON THE LOWER PART OF THE SCREEN
	RTS			;IF IN THE MIDDLE

5$:	CMP I,0C
	BCC 7$			;IF MUSHROOM IS ON LOWER HALF OF SCREEN
	CMP I,14
	BCC 10$			;IF IN THE MIDDLE
6$:	INX
	INX
7$:	DEC X,MUSH  		;CORRECT MUSHROOM COURT
10$:	RTS


	.SBTTL	MUSHER - ADD TO COUNT OF MUSHROOMS
;ENTRY	(OBST,OBST+1)=PLAYFIELD ADDRESS
;EXIT	MUSHROOM ADDED TO GIVEN LOCATION IF ONE NOT ALREADY THERE
;	(A)=PLAYFIELD STAMP
;USES	A,X,Y
MUSHER:	LDY I,0
	LDA NY,OBST
	AND I,7F
	BNE 20$			;DON'T INCREMENT IF MUSHROOM ALREADY ON SCREEN
	LDY I,7F		;FULL MUSHROOM
MUSHE1	=.
	LDA OBST
	AND I,1F
	BEQ 20$			;DON'T PUT ON SCREEN IF TOP ROW OF COCKTAIL
	CMP I,1F
	BEQ 20$			;DON'T PUT UP MUSHROOM IF ON TOP ROW
	LDX PLAYR
	BIT CKIND
	BPL 5$			;NOT TIME FOR COCKTAIL
	CMP I,1E
	BEQ 20$			;NOT ON THE SAME ROW AS THE PLAYER
	CMP I,14
	BCS 7$			;IF ON UPPER PART OF SCREEN FOR COCKTAIL
	CMP I,0C
	BCS 10$			;IF IN THE MIDDLE
	BCC 6$			;LOWER PART OF SCREEN

5$:	CMP I,01
	BEQ 20$			;NOT ON THE SAME ROW AS THE PLAYER
	CMP I,0C
	BCC 7$			;IF ON LOWER PART OF SCREEN
	CMP I,14
	BCC 10$			;IF IN THE MIDDLE
6$:	INX
	INX
7$:	INC X,MUSH  		;ADD TO MUSHROOM COUNT
10$:	TYA
	LDY I,0
	ORA NY,OBST		;LEAVE GREY BACKGROUND IF ANY
	STA NY,OBST		;PUT MUSHROOM ON SCREEN
20$:	RTS


	.SBTTL	NEWHD-PUT IN NEW HEAD
;NEWHD-PUT IN NEW HEAD AFTER DEAD=1
;
;
NEWHD:	LDA NEWD
	BEQ 40$			;NOT TIME FOR NEW HEADS
	LDA DELAY
	BNE 40$			;NOT WHEN PLAYER IS DEAD
	LDA COUNT1
	BEQ 10$
	DEC COUNT1
	RTS

10$:	LDX PLAYR
	LDY I,NCENT-1
12$:	LDA Y,MOBJC
	BEQ 20$			;IF SEGMENT IS DEAD
	DEY
	BPL 12$			;CONTINUE LOOKING FOR DEAD SEGMENT
	RTS

20$:	LDA I,0
	STA Y,MOBJP		;MAKE HEAD PICTURE
	LDA I,39
	STA Y,MOBJC		;TURN ON COLOR
	LDA I,40
	EOR CKF8
	STA Y,MOBJV		;VPOS FOR NEW HEAD
	LDA I,0FC
	STA Y,MOBJH		;HPOS FOR NEW HEAD
	LDA CKFE
	ORA I,2			;2 OR-2 DEPENDING IF COCKTAIL
	STA Y,MOBJDV		;NEW CENTIPEDE GOES FAST
	LDA X,COUNT3
	CMP I,60		;1 1/2 SECONDS
	BCC 25$			;SMALLEST VALUE WE ALLOW
	SBC I,8
	STA X,COUNT3		;DECREMENT
25$:	STA COUNT1
	LDA RND0
	AND I,02
	BNE 30$
	LDA I,4
	STA Y,MOBJH
	LDA I,-2
30$:	STA Y,MOBJDH
	INC X,DEAD		;DEAD=DEAD+1
40$:	RTS
	.PAGE
	.SBTTL	OBSTAC-OBSTACLE
;OBSTA0-SET UP ARGUMENTS FOR OBSTAC ROUTINE
;
;NOTE-THIS USES MOBJDH OF 1 OR FF
;ENTRY	(X)=INDEX OF CURRENT SEGMENT
;EXIT	(X)=INDEX OF CURRENT SEGMENT
;	(TEMP1)=X,MOBJH
;	(Y)=X,MOBJDH(1 OR FF)
;	(A)=X,MOBJV
;
OBSTA0:	LDA X,MOBJH
	STA TEMP1
	LDY I,-1
	LDA X,MOBJDH
	BMI 10$
	LDY I,1
10$:	LDA X,MOBJV

;OBSTAC-CALCULATE OBSTACLE POSITION
;
;ENTRY	(A)=MOBJV
;	(Y)=MOBJDH
;	(TEMP1)=MOBJH
;EXIT	(Z)=0=OBSTACLE
;	   =1=NO OBSTACLE
;	(Y)=0
;	(A)=PLAYFIELD STAMP CODE
;	(OBST,OBST+1)=ADDRESS OF PLAYFIELD STAMP
;USES	A,Y,TEMP1,(OBST,OBST+1)
OBSTAC:	LSR
	LSR
	LSR
	ADC I,0			;ADD ONE IF HALF WAY TO NEXT ROW
	STA OBST		;LSB OF SCREEN POSITION
	LDA I,PLYFLD/400	;BASE ADDRESS OF PLAYFIELD/4
	STA OBST+1		;MSB OF SCREEN POSITION
	TYA
	ASL
	ASL
	ASL			;8*DIRECTION
	CLC
	ADC TEMP1		;LOOK FOR OBSTACLE AT
	STA TEMP1		;(H',V')=(H+8*D,V')
	LDA I,0F7
	SEC
	SBC TEMP1
	BCS 5$			;DO NOT WRAP AROUND LEFT EDGE OF SCREEN
	LDA I,0			;USE LEFT MARGIN
5$:	AND I,0F8
	ASL
	ROL OBST+1
	ASL
	ROL OBST+1
	ORA OBST		;COMPLETE LSB
	LDY OBST+1
	CPY I,PLYFLD+3C0/100
	BNE 10$			;NOT OFF RIGHT SIDE
	CMP I,PLYFLD+3C0&0FF
	BCC 10$			;WE ARE ON PLAYFIELD
	AND I,1F
	ORA I,0A0
10$:	STA OBST		;PLAYFIELD ADDRESS=
	LDY I,0			;800+V/8+((F7-H')^F8)*4
	LDA NY,OBST		;OBSTACLE?
	AND I,7F
	RTS
	.PAGE
	.SBTTL	OVRLAP-CHECK FOR OVERLAP
;OVRLAP-CHECK FOR OVERLAPPING SEGMENTS
;
;ENTRY	(X)=INDEX OF HEAD
;EXIT	(CC)=CARRY SET IF OVERLAP OCCURS
OVRLAP:	STX TEMP1
	LDY I,NCENT
10$:	LDA Y,MOBJV
	CMP X,MOBJV
	BNE 30$			;NOT ON SAME LINE
	LDA Y,MOBJC
	BEQ 30$			;IF VACANT POSITION
	CMP I,0C0
	BCS 30$			;IF DEAD SEGMENT OR A SCORE
	CPY TEMP1
	BEQ 30$			;DO NOT COUNT US
	LDA X,MOBJH
	SEC
	SBC Y,MOBJH
	EOR X,MOBJDH		;LOOK ONLY IN FRONT OF US
	CMP I,0F4
	BCS 40$			;WE HAVE AN OVERLAP
30$:	DEY
	BPL 10$
	CLC
40$:	RTS


	.SBTTL	RESTOR-RESTORE MUSHROOMS
;
;ENTRY (MEM,MEM+1)=ADDRESS OF CURRENT PLAY FIELD VALUE
RESTOR:	LDY I,0
	LDA FRAME
	AND I,03
	BNE 5$			;EVERY 16 FRAMES
	LDA PEXPLD
	BNE 5$			;WAIT FOR PLAYER EXPLOSION TO FINISH
	LDA CDONE
	BNE 5$			;WAIT FOR GROWTH/DEATH OF MUSHROOMS TO FINISH
1$:	LDA MEM+1
	BEQ 5$			;ALL DONE
	CMP I,PLYFLD+3C0/100
	BNE 10$			;NOT DONE YET
	LDA MEM
	CMP I,PLYFLD+3C0&0FF
	BCC 10$			;NOT DONE YET
	STY MEM+1		;CLEAR POINTER
5$:	RTS

10$:	LDA NY,MEM		;LOAD PLAYFIELD
	AND I,7F
	CMP I,ROCK
	BCC 20$			;IF NOT A MUSHROOM OR A ROCK
	CMP I,7F
	BCS 20$			;IF FULL MUSHROOM
	LDA NY,MEM
	AND I,80
	ORA I,7F
	STA NY,MEM		;RESTORE TO FULL MUSHROOM
	LDA I,0
	STA TEMP1
	LDA I,5
	JSR SCORNG		;AWARD 5 PTS. FOR RESTORED MUSHROOMS
	LDA I,0FF
	STA PLAYP		;PUT MOTION OBJECT EXPLOSION ON MUSHROOM
	LDA MEM+1
	STA TEMP1
	LDA MEM
	ASL
	ROL TEMP1
	ASL
	ROL TEMP1
	ASL
	ROL TEMP1
	STA PLAYV		;EXPLOSION VERTICAL
	LDA TEMP1		;20 TO 3F
	AND I,1F		;REMOVE BASE ADDRESS OF PLAYFIELD
	EOR I,01F		;REVERSE SO LOWER LEFT CORNER IS F8
	ASL
	ASL
	ASL
	SBC I,3			;SUBTRACT 4-REMEMBER EXPLOSION IS CENTERED IN PICTURE
	STA PLAYH
	LDA I,0F9
	STA PLAYC		;TURN ON COLOR
	INC MEM
	BNE 25$			;NO OVERFLOW
	INC MEM+1
25$:	LDA I,14
	STA CHAN2
	RTS

20$:	INC MEM
	BNE 1$			;NO OVERLFLOW
	INC MEM+1
	BNE 10$			;ALWAYS-BYPASS END OF PLAYFIELD CHECK


;SCORES-DISLAY HIGH SCORES

SCORES:	LDA I,7
	JSR MESS		;"HIGH SCORES"
	LDY I,0
	LDA I,PLYFLD+15C&1F	;FOR PLAYER 1'S VIEWPOINT ONLY
	BIT CKIND
	BPL 10$			;IF UPRIGHT
	LDA I,PLYFLD+263&1F
10$:	ORA I,PLYFLD+15C&0E0
	BIT CKIND
	BPL 15$			;IF UPRIGHT
	ORA I,PLYFLD+263&0E0
15$:	STA TEMP4
	LDA I,PLYFLD+15C/100
	BIT CKIND
	BPL 20$			;IF UPRIGHT
	LDA I,PLYFLD+263/100
20$:	STA TEMP4+1		;STARTING ADDRESS FOR THIS LINE
	LDA Y,HSCORE+2
	STY TEMP2		;SAVE INDEX INTO "HSCORE"
	SEC			;ZERO SUPPRESSION
	JSR DIGIT2
	LDY TEMP2
	LDA Y,HSCORE+1
	JSR DIGIT2		;2 MORE DIGITS
	LDY TEMP2
	LDA Y,HSCORE
	CLC			;NO ZERO SUPPRESSION
	JSR DIGIT2		;DISPLAY DIGITS
	JSR NULL		;SKIP A SPACE
	LDY TEMP2
	JSR INITAL		;PUT UP INITIAL
	INC TEMP2
	LDY TEMP2
	JSR INITAL		;PUT UP 2ND INITIAL
	INC TEMP2
	LDY TEMP2
	JSR INITAL		;PUT UP 3RD INITIAL
	LDA CKFF
	EOR I,0FF		;FF IF UPRIGHT, 0 IF COCKTAIL
	ORA I,1			;FF IF UPRIGHT, 1 IF COCKTAIL
	CLC
	ADC TEMP4
	AND I,1F
	LDY TEMP2
	INY
	CPY I,3*NSCORE
	BCC 10$			;IF MORE TABLES
	JMP DDTS2		;RESTORE ANY DDT BOMBS BLANKED OUT
	.PAGE
	.SBTTL	SCORNG-SCORING
;SCORNG-AWARD SCORE
;
;ENTRY (A)=POINTS TO BE ADDED TO SCORE (1ST DIGIT)
;	(TEMP1)=POINTS TO BE ADDED TO SCORE+1 (2ND DIGIT)
;	(X)=INDEX OF CURRENT OBJECT
;
;EXIT	(X)=INDEX OF CURRENT OBJECT
;
SCORNG:	LDY MODE
	BMI 30$			;IF IN ATTRACT
	STX TEMP2		;SAVE X
	LDX PLAYR
	SED
	CLC
	ADC X,SCORE0  
	STA X,SCORE0  		;INCREMENT 1ST DIGIT OF SCORE
	LDA X,SCORE1  
	ADC TEMP1
	STA X,SCORE1  
	CLD
	BCC 15$			;IF NOT ON 10K BOUNDARY
	LDA OPTNS1
	LSR
	LDA X,COUNT3
	BCS 10$			;IF HARD SIDE FEED DECREASE COUNTER
	CMP I,31
	BCC 12$			;IF EASY STOP AT MINIMUM OF 3/8 SECONDS
10$:	SBC I,2
	STA X,COUNT3  		;INCREASE FREQUENCY OF NEW HEADS
12$:	SED
	LDA X,SCORE2  
	CLC
	ADC I,1
	STA X,SCORE2  
	CLD
15$:	LDA X,SCORE1  
	CMP X,BONUSL  
	LDA X,SCORE2  
	SBC X,BONUSM  
	BNE 25$			;NOT AT BONUS LEVEL
	JSR BONUS1		;GET BONUS LEVEL
	SED
	CLC
	ADC X,BONUSL  
	STA X,BONUSL  
	LDA Y,BONUSV+1		;SET NEXT BONUS LEVEL FOR THIS PLAYER
	ADC X,BONUSM  
	STA X,BONUSM  
	CLD
	LDA I,1
	STA X,EXTRAL		;FLAG PLAYER ACHIEVEMENT
	CPY I,06
	BEQ 25$			;IF NO BONUS IS SELECTED
	LDA X,LIVES  
	CMP I,6
	BEQ 23$			;NO MORE LIVES 6 IS MAX
20$:	BCS 20$			;OOPS-RESET
	INC X,LIVES  		;ADD A LIFE AS A BONUS
	LDA I,17.
	STA CHAN11		;BONUS LIFE SOUND
23$:	JSR DLIVES		;SHOW EXTRA LIFE AND NEXT BONUS LEVEL
25$:	LDX TEMP2		;RESTORE X
30$:	RTS
	.PAGE
	.SBTTL	SCROLL-SCROLL PLAYFIELD UP OR DOWN
;
; NO SCROLLING IS DONE IN ATTRACT BECAUSE
; THERE ARE MESSAGES AND DDT BOMBS WHICH
; WILL GET MESSED UP OTHERWISE.  NO SCROLLING
; IS DONE IF A DDT CLOUD IS PRESENT BECAUSE
; THE PLAYERS AREA, DDT'S AND MUSHROOM COUNT
; WILL GET MESSED UP.
SCROLL:	LDA MODE
	BMI 90$			;IF IN ATTRACT
	LDA CDONE
	BNE 90$			;WAIT UNTIL METAMORPHISIS IS COMPLETE
	JSR DDTS1		;GET CORRECT DDT BANK
	LDA X,DDTADD
	ORA X,DDTADD-2
	ORA X,DDTADD-4
	ORA X,DDTADD-6
	CMP I,PLYFLD+400/100
	BCS 90$			;DDT IS EXPLODING
	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BNE 90$			;PLAYER IS DEAD
	LDX PLAYR
	LDA X,HITDDT
	BNE 10$			;IF DDT HAS BEEN HIT
	LDA X,DEAD
	BEQ 10$			;IF CENTIPEDE IS DEAD
	LDA X,CENTIN
	CMP I,04
	BNE 10$			;WRONG WAVE FOR CONTINIOUS SCROLL
	LDA FRAME
	AND I,7F
	EOR I,1E
	BNE 10$			;SCROLL EVERY SO OFTEN
	DEC SCROLC
10$:	LDA SCROLC
	BEQ 90$			;NO SCROLL
	BMI SCROLD		;SCROLL DOWN
	JMP SCROLU		;SCROLL UP

90$:	RTS


	.SBTTL	SCROLD-SCROLL MUSHROOMS DOWN
;
;USES A,X,Y,(OBST,OBST+1),(TEMP1,TEMP1+1),(TEMP2,TEMP2+1)
SCROLD:	INC SCROLC
	LDA I,PLYFLD+3A0&0FF
	STA OBST
	LDA I,PLYFLD+3A0/100
	STA OBST+1
	LDY I,1E
	LDA I,0B
	LDX I,13
	BIT CKIND
	BPL 5$			;NOT COCKTAIL
	LDY I,1
	LDA I,14
	LDX I,0C
5$:	STY TEMP1+1
	STA TEMP2
	STX TEMP2+1
10$:	LDY TEMP1+1
	LDX I,0
	LDA RND1
	AND I,0F
	BNE 12$			;AND IN ONLY A FEW MUSHROOMS AT THE TOP
	LDX PLAYR
	INC X,MUSH+2		;INCREASE COUNT OF MUSHROOMS AT TOP
	LDX I,7F		;ADD A MUSHROOM NOW
12$:	BIT CKIND
	BPL 20$			;IF UPRIGHT
15$:	LDA NY,OBST
	STA TEMP1
	TXA
	STA NY,OBST		;SCROLL UP
	LDX TEMP1
	INY
	CPY I,1E
	BNE 15$			;FINISH SCROLL ON THIS COLUMN
	LDY I,19
	BNE 30$			;ALWAYS

20$:	LDA NY,OBST
	STA TEMP1
	TXA
	STA NY,OBST		;MOVE PREVIOUS ROW DOWN
	LDX TEMP1
	DEY
	CPY I,1
	BNE 20$
	LDY I,06
30$:	LDA NY,OBST		;ADD GREY AREA BACK
	ORA I,80
	CMP I,80+78
	BCC 32$			;IF NOT POISON MUSHROOM
	CMP I,80+7C
	BCS 32$			;IF NOT POISON MUSHROOM
	ORA I,04		;NORMAL MUSHROOM NOW
32$:	STA NY,OBST
	TXA
	LDX PLAYR
	AND I,7F
	CMP I,70
	BCC 35$			;IF NO MUSHROOM OR ROCK ON BOTTOM ROW
	DEC X,MUSH		;REDUCE COUNT OF MUSHROOMS
35$:	LDY TEMP2
	LDA NY,OBST
	CMP I,70
	BCC 37$			;IF ALPHANUMERIC OR BLANK
	INC X,MUSH		;ADD COUNT FOR NEW MUSHROOM
37$:	LDY TEMP2+1
	LDA NY,OBST
	CMP I,70
	BCC 39$			;IF ALPHANUMERIC OR BLANK
	DEC X,MUSH+2		;WE LOST ONE FROM THE TOP
39$:	LDA OBST
	SEC
	SBC I,20
	STA OBST
	LDA OBST+1
	SBC I,0
	STA OBST+1
	CMP I,PLYFLD/100
	BCS 10$			;CONTINUE THRU PLAYFIELD
	LDA I,0
	STA TEMP2+1		;ALLOW ONLY 1 DDT BOMB PER LINE
	JSR DDTS1		;SET  UP PARAMETERS
50$:	LDA X,DDTADD
	BEQ 60$			;IF VACANT ENTRY
	CMP I,PLYFLD+400/100
	BCS 68$			;IF EXPLODING DO NOT SCROLL
	BIT CKIND
	BPL 54$			;IF UPRIGHT
	INC X,DDTADD-1
	BNE 56$			;ALWAYS

54$:	DEC X,DDTADD-1
56$:	LDA X,DDTADD-1
	EOR CKFF
	AND I,1E
	BNE 68$			;IF STILL ON THE SCREEN
	STA X,DDTADD		;CLEAR DDT BOMB
60$:	LDA TEMP2+1
	BNE 68$			;ONLY 1 DDT BOMB PER LINE
	LDA RND1
	AND I,03
	CMP I,03
	BEQ 68$			;NO DDT BOMBS (STAY AWAY FROM THE RIGHT EDGE)
	ORA I,PLYFLD/100
	TAY
	LDA RND0
	AND I,0E0
	ORA I,1E		;ADD TO TOP ROW
	BIT CKIND
	BPL 62$			;IF UPRIGHT
	EOR I,1F
62$:	CMP I,PLYFLD+80&0FF
	BCS 65$			;IF AWAY FROM LEFT EDGE
	CPY I,PLYFLD/100
	BEQ 68$			;NO DDT BOMBS (STAY AWAY FROM LEFT EDGE)
65$:	STA X,DDTADD-1
	STA TEMP1
	TYA
	STA X,DDTADD
	STA TEMP1+1
	LDY I,20
	BIT CKIND
	BPL 64$			;IF UPRIGHT
	LDY I,0
64$:	STX TEMP2+1		;NON-ZERO=FLAG FOR NO MORE DDT BOMBS
	LDX PLAYR
	LDA NY,TEMP1
	BEQ 66$			;NO MUSHROOM HERE
	DEC X,MUSH+2		;ONE LESS MUSHROOM
66$:	LDA I,DDT+1
	STA NY,TEMP1
	TYA
	EOR I,20
	TAY
	LDA NY,TEMP1
	BEQ 67$			;NO MUSHROOM HERE
	DEC X,MUSH+2		;ONE LESS MUSHROOM
67$:	LDA I,DDT
	STA NY,TEMP1
	LDX TEMP2+1
68$:	DEX
	DEX
	DEC TEMP2
	BPL 50$			;CONTINUE THRU ALL BOMBS
SCROL0	=.
	LDA PLAYH
	STA TEMP1
	LDY I,0
	LDA PLAYV
	JSR OBSTAC		;CHECK FOR OBSTACLE ON PLAYER
	BEQ 90$			;NO OBSTACLE
	LDX PLAYR		;PREVENT PLAYER GETTING STUCK
	DEC X,MUSH		;ONE LESS MUSHROOM
	LDA I,80
	STA NY,OBST		;DELETE OBSTACLE
90$:	RTS

	.SBTTL	SCROLU-SCROLL MUSHROOMS UP
;
;USES A,X,Y,(OBST,OBST+1),(TEMP1,TEMP1+1),(TEMP2,TEMP2+1)
SCROLU:	DEC SCROLC
	LDA I,PLYFLD+3A0&0FF
	STA OBST
	LDA I,PLYFLD+3A0/100
	STA OBST+1
	LDY I,02
	LDA I,0C
	LDX I,14
	BIT CKIND
	BPL 5$			;NOT COCKTAIL
	LDY I,1D
	LDA I,13
	LDX I,0B
5$:	STY TEMP1+1
	STA TEMP2
	STX TEMP2+1
10$:	LDY TEMP1+1
	LDX I,80
	BIT CKIND
	BPL 20$			;IF UPRIGHT
15$:	LDA NY,OBST
	STA TEMP1
	TXA
	STA NY,OBST
	LDX TEMP1
	DEY
	BNE 15$			;COPY THE WHOLE COLUMN
	LDY I,18
	BNE 30$			;ALWAYS

20$:	LDA NY,OBST
	STA TEMP1
	TXA
	STA NY,OBST		;MOVE PREVIOUS ROW UP
	LDX TEMP1
	INY
	CPY I,1F
	BNE 20$
	LDY I,07
30$:	LDA NY,OBST		;REMOVE SCROLLED GREY AREA
	AND I,7F
	STA NY,OBST
	TXA
	LDX PLAYR
	AND I,7F
	CMP I,70
	BCC 35$			;IF NO MUSHROOM OR ROCK ON TOP ROW
	DEC X,MUSH+2		;REDUCE COUNT OF MUSHROOMS
35$:	LDY TEMP2
	LDA NY,OBST
	CMP I,70
	BCC 37$			;IF ALPHANUMERIC OR BLANK
	DEC X,MUSH		;CHANGE COUNT FOR LOSS OF MUSHROOM
37$:	LDY TEMP2+1
	LDA NY,OBST
	CMP I,70
	BCC 39$			;IF ALPHANUMERIC OR BLANK
	INC X,MUSH+2		;WE ADDED ONE TO THE TOP
39$:	LDA OBST
	SEC
	SBC I,20
	STA OBST
	LDA OBST+1
	SBC I,0
	STA OBST+1
	CMP I,PLYFLD/100
	BCS 10$			;CONTINUE THRU PLAYFIELD
	JSR DDTS1		;SET UP PARAMETERS
50$:	LDA X,DDTADD
	BEQ 58$			;IF EMPTY ENTRY
	CMP I,PLYFLD+400/100
	BCS 58$			;IF EXPLODING
	BIT CKIND
	BPL 54$			;IF UPRIGHT
	DEC X,DDTADD-1
	JMP 56$

54$:	INC X,DDTADD-1		;MOVE POINTER
56$:	LDA X,DDTADD-1
	EOR CKFF
	AND I,1F
	EOR I,1F
	BNE 58$			;IF NOT OFF TOP
	STA X,DDTADD		;CLEAR ENTRY
58$:	DEX
	DEX
	DEC TEMP2
	BPL 50$
	JMP SCROL0		;REMOVE OBSTACLE ON TOP OF PLAYER.



	.SBTTL	SELECT - SELECT STARTING SCORE
;
;EXIT	(CC)=CARRY SET IF IN SELECT MODE
SELECT:	LDA MODE
	CLC			;LET GAME PLAY CONTINUE
	BEQ 40$			;IF WE HAVE NOTHING TO DO
	BPL 50$			;IF WE ARE STARTING A GAME
	LDA TIMER
	BEQ 40$			;NOTHING TO DO
	LDA OPTNS1
	BMI 30$			;IF NO SELECT MODE
	JSR BONUSS		;GET BONUS LEVELS
	CMP LSCORE
	BNE 20$			;IF NOT A MIN
	LDA X,BONUSV+1
	CMP LSCORE+1
	BEQ 30$			;IF AT MIN
20$:	SEC
	ROR HILITE		;SET HIGHLIGHT
	LDA FRAME
	AND I,10
	EOR I,10		;BE SURE WE CLEAR MESSAGE WHEN TIMER RUNS OUT
	ASL
	ASL
	ASL
	STA TEMP2
	ORA I,15.
	JSR MESS		;FLASH "FOR ADVANCED GAME"
	LDA I,16.		;FLASH "INSERT COIN"
	LDY $$CRDT
	BEQ 25$			;IF NO CREDITS
	LDA I,17.		;ELSE FLASH "PRESS START"
25$:	ORA TEMP2
	JSR MESS		;DISPLAY OR ERASE MESSAGE
	ASL HILITE		;CLEAR HIGHLIGHT
30$:	LDA FRAME		;IF IN ATTRACT DECREASE TIMER
	AND I,3F
	BNE 35$			;DO NOT DO ANYTHING YET
	DEC TIMER
	BNE 35$			;IF TIME REMAINS
	JSR SELEC4		;RESET MAXIMUM STARTING SCORE
	JSR INITFF		;INITIALIZE FOR ATTRACT
35$:	SEC			;FREEZE SCREEN UNTIL TIMER RUNS OUT
40$:	RTS

50$:	LDA FRAME
	AND I,3F
	BNE 60$			;NOT ON A "SECOND" BOUNDARY
	DEC TIMER		;DECREASE TIMER
	BEQ 68$			;NO TIME LEFT
60$:	LDA TIMER
	CMP I,3
	BCS 62$			;PLENTY OF TIME LEFT
	LDA I,1
	STA CHAN7		;TURN ON SOUND
	LDA FRAME
	AND I,4
	BEQ 61$			;TURN SOUND ON AND OFF
	LDA I,20
61$:	STA AUDF1+6		;SET FREQUENCY
62$:	LDA I,12.
	JSR MESS		;DISPLAY "TIME"
	LDA TIMER
	ORA I,20
	JSR CHAR		;DISPLAY TIMER VALUE
	LDA FIRE1
	BIT CKIND
	BPL 65$			;IF NOT PLAYER 2 COCKTAIL
	LDA FIRE2
65$:	AND I,10
	CMP I,10
	ROL SDBNCE
	LDA SDBNCE		;DEBOUNCE AND LOOK FOR AN EDGE
	AND I,1F
	CMP I,18
	BNE 70$			;IF FIRE IS NOT PRESSED
68$:	DEC MODE		;MODE=0
	JSR INITSC		;INITIALIZE THE SCREEN AND PLAYER
	LDX PLAYR
	LDA X,SCORE2
	ASL
	ASL
	SEC
	ADC X,SCORE2		;1 TO 0F0
	TAX			;NUMBER OF OTHER MUSHROOMS TO ADD
	LDY I,08		;BOTTOM ROW FOR MUSHROOMS
	LDA I,17		;TOP ROW FOR MUSHROOMS
	JSR INIT3		;ADD MORE MUSHROOMS
	JSR CENTPC		;NEW CENTIPEDE IN CASE WE NOW HAVE 8 SPIDERS
	JSR BONUS2		;DISPLAY NEXT BONUS LEVEL
	JSR DLIVES		;DISPLAY THE LIVES
	LDA I,0
	STA DELAY		;START THE GAME IMMEDIATELY
	STA SWTCHP		;WE WILL CLEAR "PLAYER" MESSAGE OURSELVES
	CLC
	RTS

70$:	JSR BONUS1		;GET CURRENT BONUS LEVEL
	LDX I,0
	LDA TB
	STX TB			;CLEAR COUNTER
	LDX PLAYR
	CLC
	ADC PLAYH
	EOR CKFF
	CMP I,0C0
	BCC 75$			;IF NOT ON THE LEFT
	CMP I,0E0
	BCC 72$			;IF BELOW THE MAX
	LDA I,0DF
72$:	BIT SSCORE+1
	BPL 75$			;IF IT IS OK TO MOVE LEFT
	LDA I,0BF		;DO NOT LET HIM MOVE THERE
75$:	CMP I,040
	BCS 80$			;IF NOT OFF TO THE RIGHT
	CMP I,20
	BCS 77$			;IF ABOVE MIN
	LDA I,20
77$:	BIT SSCORE+7
	BPL 80$			;IT IS OK TO MOVE RIGHT
	LDA I,40
80$:	CMP I,0C0
	BCC 85$			;IF NOT TIME TO SHIFT ALL NUMBERS
	LDX SSCORE+1
	BEQ 85$			;WE ARE AT THE MIN NOW
	PHA
	LDX I,5
82$:	LDA X,SSCORE
	STA X,SSCORE+2
	DEX
	BPL 82$			;MOVE EVERYTHING UP
	SED
	LDA SSCORE+2
	SEC
	SBC Y,BONUSV
	STA SSCORE
	LDA SSCORE+3
	SBC Y,BONUSV+1
	STA SSCORE+1
	CLD
	PLA
	SEC
	SBC I,40		;MOVE PLAYER BACK
85$:	CMP I,40
	BCS 90$			;IF PLAYER IS NOT IN THE FAR RIGHT AREA
	LDX SSCORE+7
	CPX LSCORE+1
	BEQ 90$			;WE ARE AT THE MAX
	PHA
	LDX I,0
87$:	LDA X,SSCORE+2		;MOVE ALL ENTRIES DOWN
	STA X,SSCORE
	INX
	CPX I,6
	BCC 87$
	SED
	LDA SSCORE+4
	CLC
	ADC Y,BONUSV
	STA SSCORE+6		;GET
	LDA SSCORE+5
	ADC Y,BONUSV+1
	STA SSCORE+7
	CLD
	PLA
	CLC
	ADC I,40		;MOVE PLAYERS SHOOTER
90$:	EOR CKFF
	STA PLAYH
	STA SHOTH
	STY TEMP1		;SAVE INDEX TO BONUS LEVEL
	EOR CKFF
	AND I,0C0
	EOR I,0C0
	LSR
	LSR
	LSR
	LSR
	LSR
	TAY			;INDEX FOR PLAYERS
	LDX PLAYR
	LDA X,SCORE2
	CMP Y,SSCORE+1
	BEQ 92$			;NO CHANGE IN PLAYERS SELECTION
	LDA I,20
	STA AUDF1+6
	LDA I,2
	STA CHAN7		;AUDIO INDICATION
	LDA Y,SSCORE+1
	STA X,SCORE2
	LDA Y,SSCORE
	STA X,SCORE1		;NEW PLAYERS SCORE
	SED
	LDY TEMP1
	CLC
	ADC Y,BONUSV
	STA X,BONUSL
	LDA X,SCORE2
	ADC Y,BONUSV+1
	STA X,BONUSM		;NEW BONUS LEVEL
	CLD
	JSR BONUS2		;DISPLAY IT
92$:	LDA I,PLYFLD+5&0FF
	LDX I,PLYFLD+5/100
	BIT CKIND
	BPL 94$			;IF UPRIGHT CABINET
	LDA I,PLYFLD+3BA&0FF	;COCKTAIL
	LDX I,PLYFLD+3BA/100
94$:	STA TEMP4
	STX TEMP4+1
	LDX I,0
96$:	LDA X,SSCORE+1
	BPL 97$			;IF A VALID ENTRY
	ASL
	BCS 99$			;ALWAYS AND A=0

97$:	LDY PLAYR
	CMP Y,SCORE2
	BEQ 98$			;HIGHLITE PLALYERS SELECTION
	CLC
98$:	ROR HILITE		;SET/CLEAR HIGHLITE
	SEC
99$:	JSR DIGIT2		;DISPLAY 2 DIGITS
	LDA X,SSCORE
	JSR DIGIT2		;DISPLAY 2 DIGITS
	LDA X,SSCORE+1
	ASL			;SET CARRY IF WE ARE TO BLANK THIS ENTRY
	LDA I,0
	JSR DIGIT2		;DISPLAY 2 DIGITS
	JSR NULL		;SKIP A SPACE
	JSR NULL		;SKIP A SPACE
	ASL HILITE		;CLEAR HILITE FLAG
	INX
	INX
	CPX I,08
	BCC 96$
	RTS


SELEC1:	LDA MODE
	BEQ 50$			;NO SELECT MODE
	LDA I,9
	STA TIMER		;START TIMER
	LDA I,0FF
	STA BKGND		;CLEAR GREY FROM PLAYERS AREA
	LDA I,0
	STA SDBNCE		;INITIALIZE FOR CHECKING THE FIRE SW
	LDY NPLAYR
	DEY
	BEQ 10$			;IF 1 PLAYER GAME
	LDA I,0
	JSR MESS		;DISPLAY "PLAYER"
	LDA PLAYR
	CLC
	ADC I,21
	JSR CHAR		;DISPLAY 1 OR 2
10$:	LDA I,13.
	JSR MESS		;DISPLAY "PRESS FIRE TO START"
	LDA I,14.
	JSR MESS		;DISPLAY "SELECT STARTING SCORE
	LDA I,20
	EOR CKFF
	STA PLAYV		;POSITION PLAYER UNDER SCORE OF 0
	STA SHOTV
	LDA I,0A0
	EOR CKFF
	STA PLAYH
	STA SHOTH
	LDA I,80
	STA SSCORE+1		;SET FIRST ENTRY EMPTY
	LDA I,0
	STA SSCORE
	STA SSCORE+2
	STA SSCORE+3		;NEXT ENTRY IS A SCORE OF 0
	JSR BONUS1
	STA SSCORE+4
	LDA Y,BONUSV+1
	STA SSCORE+5
	TYA
	ASL
	ASL
	ADC I,12.
	TAY			;INDEX FOR 2*BONUS
	LDA Y,BONUSV
	STA SSCORE+6
	LDA Y,BONUSV+1
	CMP LSCORE+1
	BCC 20$			;IF LAST ENTRY IS VALID
	BEQ 20$			;IF LAST ENTRY IS VALID
	LDA I,0
	STA SSCORE+6
	LDA I,80
20$:	STA SSCORE+7
50$:	RTS

SELEC2:	LDX I,0
	LDA SCORE0
	CMP SCORE0+1
	LDA SCORE1
	SBC SCORE1+1
	LDA SCORE2
	SBC SCORE2+1
	BCS 10$			;PLAYER 1 HAS A HIGHER SCORE
	INX			;ELSE PLAYER 2 DOES
10$:	JSR BONUS1		;GET BONUS VALUE NEEDED
	LDA I,1
	STA TEMP1
12$:	LDA X,BONUSL
	SED
	SEC
	SBC Y,BONUSV
	STA LSCORE		;SET STARTING VALUE
	STA X,BONUSL
	LDA X,BONUSM
	SBC Y,BONUSV+1
	BCS 20$			;IF SCORE WAS NOT TOO SMALL
	LDA I,0
	STA LSCORE
	STA X,BONUSL
20$:	STA LSCORE+1
	STA X,BONUSM
	CLD
	DEC TEMP1
	BPL 12$			;DECREASE BONUS VALUE TO CORRECT LEVEL
	CMP I,30
	BCC 30$			;IF SCORE EXCEEDS 300,000
	LDA I,0			;RESET STARTING VALUES TO 300,000
	STA LSCORE		;NOTE THAT BONUS LEVELS MUST DIVIDE 300,000 EVENLY
	LDA I,30
	STA LSCORE+1
30$:	LDA I,30.
	STA TIMER		;RESET TIMER FOR 30 SECONDS
SELEC3:	JSR BONUSS		;GET MAX BONUS
	LDA LSCORE+1
	CMP X,BONUSV+1
	BCS 50$			;IF GREATER THAN OR EQUAL TO MIN SETTING
SELEC4	=.
	JSR BONUSS		;GET MAX SETTING
	STA LSCORE
	LDA X,BONUSV+1
	STA LSCORE+1
50$:	RTS
	.PAGE

	.SBTTL	SWAP-SWAPSCREENS
;SWAP-SWAP SCREENS IN TWO-PLAYER GAME
;
SWAP:	LDY I,0
	STY OBST
	LDA I,PLYFLD/100
	STA OBST+1		;STARTING ADDRESS OF PLAYFIELD
	STY TEMP2		;TEMP2=INDEX INTO "STORE"
5$:	LDA I,0
	STA TEMP1
	STY TEMP2+1
	LDX I,8
10$:	LDA NY,OBST		;BUILD A BYTE FOR NEXT 8 PLAYFIELD LOCATIONS
	AND I,7F
	CMP I,7C
	ROL TEMP1
	INY
	DEX
	BNE 10$
	LDX TEMP2
	LDA AX,STORE		;EXCHANGE CURRENT WITH OTHER PLAYERS "BYTE"
	INC TEMP2
	TAY
	LDA TEMP1
	STA AX,STORE
	STY TEMP1
	LDY TEMP2+1		;RESTORE Y
	LDX I,8
20$:	LDA I,0			;ADD NEXT PLYERS 8 PLAYFIELD STAMPS
	ROL TEMP1
	BCC 25$			;IF SPACE WAS EMPTY
	LDA I,7F		;INSERT MUSHROOM
25$:	STA NY,OBST
	INY
	DEX
	BNE 20$
	TYA
	BNE 30$			;NO OVERFLOW
	INC OBST+1
30$:	CPY I,0C0
	BNE 5$			;MORE PLAYFIELD TO COPY
	LDA OBST+1
	CMP I,PLYFLD+3C0/100
	BNE 5$			;STOP AT ADDRESS 7C0
	JSR DDTS2		;ADD BACK DDT BOMBS
	INC LCOLOR		;RESET COLORS FOR CURRENT PLAYER
	JMP INIT2		;ADD PLAYERS GREY AREA


	.SBTTL	TBLMT-LIMIT TRACKBALL MOVEMENT
;TBLMT-LIMIT TRACKBALL MOVEMENT
;
;EXIT (Y)=TRACK BALL READING(MSB)
;(A)=LSB'S OF TRACKBALL
TBLMT:	CMP I,08
	BCC 10$			;WITHIN RANGE
	CMP I,0F8
	BCS 10$			;WITHIN RANGE
	CMP I,80
	LDA I,08		;LIMIT RANGE TO 8
	BCC 10$
	LDA I,0F8		;LIMIT TO -8
10$:	CMP I,80		;SIGNED DIVIDE BY 2
	ROR
	TAY
	LDA I,0
	ROR			;SET LSB'S (0 OR 80)
	RTS
	.PAGE
	.SBTTL	UPDATE-UPDATE HIGH SCORE TABLE
;UPDATE-UPDATE HIGH SCORE TABLE

UPDATE:	LDA I,-1
	LDX I,1
	STA UPDFLG
	STA UPDFLG+1		;CLEAR FLAGS
	LDA $CMODE
	AND I,0C3
	CMP I,0C0
	BEQ 90$			;IF IN DEMO MODE DO NOT COUNT THIS GAME
	SED
	LDA GTIME		;ACCUMULATE GAME TIME
	CLC
	ADC EATIME
	STA EATIME
	LDA GTIME+1
	ADC EATIME+1
	STA EATIME+1
	LDA EATIME+2
	ADC I,0
	STA EATIME+2
	LDA EATIME+3
	ADC I,0
	BCS 5$			;DO NOT OVERFOW COUNTER
	STA EATIME+3
	LDA EAGAMS		;UPDATE NUMBER OF PLAYS
	CLC
	ADC NPLAYR
	STA EAGAMS
	LDA EAGAMS+1
	ADC I,0
	STA EAGAMS+1
	LDA EAGAMS+2
	ADC I,0
	STA EAGAMS+2		;IF THIS OVERFLOWS EA ROM IS DEAD
5$:	CLD
18$:	LDY I,0			;INDEX INTO HIGH SCORE TABLE
	LDA X,EXTRAL
	BEQ 28$			;PLAYER DID NOT GET AN EXTRA LIFE
20$:	LDA X,SCORE0
	CMP Y,HSCORE		;SETS CARRY
	LDA X,SCORE1
	SBC Y,HSCORE+1
	LDA X,SCORE2
	SBC Y,HSCORE+2
	BCS 30$			;NEW HIGH SCORE
25$:	INY
	INY
	INY
	CPY I,3*NSCORE
	BCC 20$			;LOOP FOR ALL EXTRIES
28$:	DEX
	BPL 18$			;NEXT PLAYER
	LDA UPDFLG+1
	BMI 29$			;NO PLAYER 2 HIGH SCORE
	CMP UPDFLG
	BCC 29$			;IF PLAYER 2 SCORE IS BETTER THAN PLAYER 1
	ADC I,02		;ADD 3
	CMP I,3*NSCORE
	BCC 27$			;IF NOT OUT OF TABLE NOW
	LDA I,-1
27$:	STA UPDFLG+1		;NEW INDEX
29$:	LDA I,0
	STA UPDINT		;STARTING WITH FIRST INITIAL
90$:	RTS

30$:	STX TEMP2		;SAVE INDEX
	STY TEMP2+1
	STY X,UPDFLG		;FLAG TO GET PLAYERS INITIALS
	LDX I,3*NSCORE-1
40$:	LDA X,INITL-3		;COPY INITIALS DOWN
	STA X,INITL
	LDA X,HSCORE-3
	STA X,HSCORE		;COPY HIGH SCORE
	DEX
	CPX TEMP2+1
	BNE 40$			;LOOP UNTIL ENTRIES MOVED UP
	LDA I,1
	STA X,INITL		;START LETTERS AT A
	LDA I,0			;CLEARS SECOND AND THIRD INITIALS
	STA X,INITL+1
	STA X,INITL+2
	STA TB			;CLEAR ANY EXTRA VALUES SO INITIALS START AT "A"
	LDX TEMP2		;LOOP FOR NEXT PLAYER
	LDA X,SCORE2		;MOVE HIGH SCORES IN
	STA Y,HSCORE+2
	LDA X,SCORE1
	STA Y,HSCORE+1
	LDA X,SCORE0
	STA Y,HSCORE
	LDA I,0F0		;1 MINUTE AT 60HZ
	STA FRAME+1		;PREPARE TO TIMEOUT GETTING INITIALS
	BNE 28$			;ALWAYS
	.PAGE
	.SBTTL	UPSCRE-UPDATE SCORES ON SCREEN
;UPSCRE-UPDATE SCORES AT TOP OF SCREEN
;
UPSCRE:	LDA I,PLYFLD+1F&0FF
	LDY I,PLYFLD+1F/100
	BIT CKIND
	BPL 5$			;IF UPRIGHT
	LDA I,PLYFLD+3A0&0FF
	LDY I,PLYFLD+3A0/100
5$:	STA TEMP4
	STY TEMP4+1		;PLAYFIELD ADDRESS
	LDA SCORE2
	SEC			;ZERO SUPPRESSION
	JSR DIGIT2
	LDA SCORE1
	JSR DIGIT2		;2 MORE DIGITS
	LDA SCORE0
	CLC			;NO ZERO SUPPRESSION
	JSR DIGIT2		;UPDATE PLAYER ONE'S SCORE
	LDY NPLAYR
	DEY
	BEQ 10$			;IF NOT 1 PLAYER
	LDY I,PLYFLD+31F/100
	LDA I,PLYFLD+31F&0FF	;INDEX FOR PLAYFIELD ADDRESS
	BIT CKIND
	BPL 8$			;IF UPRIGHT
	LDA I,PLYFLD+0A0&0FF
	LDY I,PLYFLD+0A0/100
8$:	STY TEMP4+1
	STA TEMP4
	LDA SCORE2+1
	SEC
	JSR DIGIT2
	LDA SCORE1+1
	JSR DIGIT2		;2 MORE DIGITS
	LDA SCORE0+1
	CLC
	JSR DIGIT2		;UPDATE PLAYER TWO'S SCORE
10$:	LDA I,PLYFLD+19F&0FF
	LDY I,PLYFLD+19F/100
	BIT CKIND
	BPL 15$			;IF UPRIGHT
	LDA I,PLYFLD+220&0FF
	LDY I,PLYFLD+220/100
15$:	STA TEMP4
	STY TEMP4+1
	LDA HSCORE+2
	SEC
	JSR DIGIT2		;DISPLAY 2 DIGITS OF HIGH SCORE
	LDA HSCORE+1
	JSR DIGIT2		;2 MORE DIGITS
	LDA HSCORE
	CLC
	JMP DIGIT2		;DISPLAY 2 DIGITS OF HIGH SCORE


CKSUM2:	.BYTE 084		;CHECKSUM FOR THIRD 4K BLOCK

	.REPT 019		;FILL REST OF ADDRESS SPACE
	.BYTE 0FF
	.ENDR

	.END
                                                                                                                                                                                                                                                                                                                                   