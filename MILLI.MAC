	.TITLE MILLI
	.INCLUDE MLDEF
	.PAGE
	.ASECT
	.SBTTL	MAINLOOP
	.=PROG

	JMP RESET

CPYRHT:	.ASCIN /    COPYRIGHT ATARI 1982    /
MAIN:	JSR INIT		;INITIAIZE EVERYTHING
	CLI			;ENABLE IRQ
	JSR INITFF		;INITIALIZE ATTRACT MODE
1$:	LSR SYNC
	BCC 1$			;WAIT FOR IRQ TO GET TO VBLANK
	STA WTCHDG		;WRITE TO WATCHDOG
	BIT SLFTST
3$:	BPL 3$			;IN SELF-TEST WAIT FOR WATCHDOG
	JSR CHKST		;CHECK FOR GAME START
	JSR UPSCRE		;UPDATE SCORES AT TOP OF PLAYFIELD
	JSR GETINT		;GET INITIAL FOR HIGH SCORE TABLE
	BPL 1$			;IF WE ARE ENTERING INITIALS
	JSR WRITEA		;WRITE AND INITIALIZE EA ROM
	JSR SELECT		;PROCESS SELECT STARTING SCORE
	BCS 1$			;WAIT UNTIL DONE
	JSR EXPLOD		;EXPLODE SEGMENTS AND PLAYER
	JSR MOVE		;MOVE PLAYER
	JSR BOMBS		;BOMB PLAYER WITH BEE, DRAGONFLY, ...
	JSR SHOOT		;FIRE SHOTS AND CHECK FOR COLLISIONS
	JSR NEWHD		;IS IT TIME FOR NEW HEADS
	LDA SLOW
	BEQ 10$			;NO SLOW DOWN
	JSR SLOWDN		;PROCESS SLOW DOWN MODE
	BCS 20$			;NO MOVEMENT THIS FRAME
10$:	JSR MOTION		;MOVE CENTIPEDES
	JSR SPDMV		;ENTER AND MOVE SPIDERS
	JSR FLYMV		;ENTER AND MOVE DRAGONFLY
	JSR EARWIG		;MOVE AND START EARWIG
	JSR BEEMV		;MOVE BEE DOWN SCREEN
	JSR MOSQT		;ENTER AND MOVE A MOSQUITO
	JSR BEETL		;MOVE AND START BEETLES
	JSR WRMMV		;ENTER AND MOVE INCH WORM
20$:	JSR MODEFF		;ATTRACT MODE DRIVER
	JSR CHKEND		;CHECK FOR END OF GAME
	JSR RESTOR		;RESTORE MUSHROOMS
	JSR SCROLL		;CHECK FOR SCROLLING
	LDA CDONE
	BEQ 1$			;IF NO GROWTH/DEATH OF MUSHROOMS
	JSR MASTER
	JMP 1$

CKSUM0:	.BYTE 0EA		;CHECKSUM FIRST 4K
	.PAGE


	.SBTTL	BEEMV-MOVE BEE DOWN SCREEN
;BEEMV--MOVE BEE IF FEW MUSHROOMS ON SCREEN
;
;
BEEMV:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BEQ 2$			;IF PLAYER IS ALIVE
	RTS

2$:	LDA BEEC+12.
	BNE 5$			;IF ENTRY IS ALREADY TAKEN
	LDX PLAYR
	LDA X,CENTIN  
	LDY X,DEAD
	BNE 3$			;IF CENTIPEDE IS NOT DEAD
	LDY BEETLS
	BEQ 3$			;IF NO BEETLES
	CMP I,10.
	BCS 4$			;IF TOO EARLY FOR BEES
3$:	JSR BEEMV1		;GET COUNT OF MUSHROOMS NEEDED
	CMP X,MUSH  
	BCC 5$			;IF THERE ARE MANY MUSHROOMS NEAR BOTTOM
4$:	LDX I,12.
	JSR BEEMV3
5$:	LDX I,12.
	LDA I,0FF
	STA TEMP1+1		;HIGHEST FREQUENCY FOR SOUND
10$:	LDA X,BEEC
	BEQ 30$			;NO OBJECT HERE
	LDA X,BEEP
	CMP I,3A
	BCS 30$			;NOT AN BEE
	CMP I,38
	BCC 30$			;NOT AN BEE
	LDA FRAME
	AND I,01
	BNE 20$			;SLOW DOWN PICTURE CHANGES
	LDA X,BEEP
	EOR I,01		;FLAP THE WINGS
	STA X,BEEP		;CHANGE PICTURE
20$:	LDA X,BEEV		;SOUND FOR BEE
	EOR CKFF
	LSR
	EOR I,0FF
	ORA I,80
	CMP TEMP1+1
	BCS 25$			;THERE IS A LOWER SOUND
	STA TEMP1+1
25$:	LDA I,1
	STA CHAN7		;TURN ON SOUND
	JSR BEEMV0		;MOVE BEE AND CHECK COLLISIONS
	BCC 50$			;COLLISION WITH PLAYER
30$:	DEX
	BPL 10$			;TRY NEXT ENTRY
	LDA TEMP1+1
	STA AUDF1+6		;SET FREQUENCY
50$:	RTS


;
; ENTRY	(X)=MOTION OBJECT INDEX
; EXIT	(CC)=CARRY CLEAR IF COLLISION WITH PLAYER
; USES	A,Y,TEMP1,TEMP2
BEEMV0:	LDA X,BEEH
	STA TEMP1
	LDA X,BEEV
	SEC
	SBC X,BEEDV
	STA X,BEEV
	EOR CKF8
	CMP I,4
	BCC BEEOFF		;IF BOTTOM OF SCREEN
	JSR PLAY		;CHECK FOR COLLISION
	BCC 30$			;IF PLAYER IS DEAD
	LDA I,4			;MAKE MUSHROOM APPEAR AFTER BEE
	EOR CKFF		;4 OR -5 FOR PLAYER 1 OR 2 COCKTAIL
	CLC
	ADC X,BEEV
	LDY I,0
	JSR OBSTAC		;GET PLAYFIELD ADDRESS
	JSR DDTEXP		;CHECK FOR DDT EXPLOSION
	BCS 30$			;IF BEE DIED IN DDT CLOUD
	BIT MODE
	BMI 10$			;IF IN ATTRACT ADD MUSHROOMS
	LDA NOCENT
	BNE 28$			;NO MUSHROOMS IN "BOMB" MODE
10$:	LDA FRAME
	AND I,03
	BNE 28$			;PUT UP MUSHROOMS ONLY EVERY FOUR FRAMES
	LDY X,BEEP
	CPY I,20
	BCS 26$			;IF A BEE
	LDA X,BEEV		;ELSE A DRAGONFLY
	EOR CKFF
	CMP I,48
	LDA I,01
	BCS 27$			;IF ABOVE PLAYERS AREA
	LDA I,07		;LESS MUSHROOMS IN PLAYERS AREA
	BNE 27$			;ALWAYS

26$:	LDA I,3
27$:	AND RND1
	BNE 28$			;DO NOT PUT UP MUSHROOM
	STX TEMP1
	JSR MUSHER		;PUT MUSHROOMS ON SCREEN
	LDX TEMP1
28$:	SEC
30$:	RTS

BEEOFF:	LDA I,0
	STA X,PTS
10$:	STA X,BEEC		;NO COLORS
	STA X,BEEH		;PREVENTS BLANKING OTHER MOTION OBJECTS
	SEC			;SO "BEEMV0" WILL RETURN CORRECT STATUS
	RTS


; BEEMV1 - GET COUNT OF MUSHROOMS NEEDED
;
;ENTRY	(X)=PLAYER NUMBER
;EXIT	(A)=COUNT OF MUSHROOMS NEEDED (5 TO 30)
;USES	A,Y
BEEMV1:	LDA X,SCORE2
	CMP I,2
	LDY I,05
	BCC 3$			;MORE BEES AT HIGHER SCORES
	LDY I,09
3$:	CMP I,12
	BCC 8$			;NOT OVER 120K IN SCORE
	LSR			;EVERY 20K
	CLC
	ADC I,6			;INCREASE MINIMUM NUMBER OF MUSHROOMS
	CMP I,30
	BCC 5$			;IF BELOW MAX NUMBER ALLOWED
	LDA I,2F
5$:	TAY
8$:	TYA
	RTS


BEEMV3:	STX TEMP1
	LDX PLAYR
	LDA I,3
	LDY X,SCORE2
	LDX TEMP1
	CPY I,6
	BCS 12$			;BEE GOES FASTER AT 60K
	LDY I,ATTRT2-ATTRT1-3	;INCLUDE OPCODE FOR JSR
SECUR1	=.+1
	LDA I,57		;EXPECTED CHECKSUM
60$:	EOR Y,ATTRT1
	DEY
	BPL 60$			;SCREW UP THE WORKS
	TAY
	BEQ 70$			;EVERYTHING IS FINE
	LDX PLAYR
	LDA X,SCORE0
	LDX TEMP1
	CMP I,90
	BCC 70$			;NOT ALL THE TIME
	SED
70$:	LDA I,2
12$:	BIT CKIND
	BPL 13$			;IF NOT UPSIDE DOWN
	JSR COMP
13$:	STA X,BEEDV
	LDA I,38
	STA X,BEEP		;PICTURE
BEEMV2:	LDA I,0F8
	EOR CKF8		;0 OR F8
	STA X,BEEV		;VPOS
10$:	LDA RND0		;WILL HANG IF POKEY IS GONE AND D0-D7=0'S OR 1'S
	AND I,0F8
	BEQ 10$			;OFF SCREEN
	CMP I,10
	BCC 10$			;OFF SCREEN
	SEC
	SBC I,04		;WE WANT
	STA X,BEEH		;HPOS
	LDA I,0
	STA X,BEEDH		;HDIR
	LDA I,79
	STA X,BEEC		;TURN ON COLOR
	RTS


	.SBTTL	BEETL-MOVE AND START BEETLE
BEETL:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BEQ 5$			;IF PLAYER IS NOT DEAD
	RTS

5$:	LDA BEETLA
	BEQ 50$			;NO MORE BEETLES ALLOWED
	LDX PLAYR
	LDA X,SCORE2
	CMP I,60
	LDA I,7F
	BCC 6$			;EVERY TWO SECONDS UP TO 600,000
	LDA I,3F		;EVERY SECOND THEREAFTER
6$:	AND FRAME
	EOR I,37
	BNE 50$			;NOT TIME TO START A BEETLE
	LDX PLAYR
	LDA X,DEAD
	BEQ 50$			;IF CENTIPEDE IS GONE, DO NOT ADD MORE BEETLES
	LDA X,CENTIN
	CMP I,12.
	BCS 50$			;NO BEETLES WHEN CENTIPEDE IS FULL
	LDA OPTNS1
	AND I,02
	BNE 7$			;IF HARD
	LDA NEWD
	BNE 50$			;IF EASY AND SIDE-FEED IS ACTIVE THEN NO BEETLES
7$:	LDA X,SCORE2
	CMP I,9
	LDX I,01
	BCC 8$			;ALLOW 1 BEETLE AT A TIME UP TO 90,000
	INX
	CMP I,25
	BCC 8$			;ALLOW 2 BEETLES FROM 90,000 TO 250,000
	INX			;ALLOW 3 AFTER THAT
8$:	CPX BEETLS
	BEQ 50$			;IF ENOUGH BEETLES ALREADY
	LDX I,NCENT
10$:	DEX
	BMI 50$			;NO ENTRY AVAILABLE
	LDA X,MOBJC
	BNE 10$			;WE HAVE NOT FOUND AN ENTRY
	INC BEETLS		;INCREASE COUNT OF BEETLES
	DEC BEETLA		;DECREASE COUNT ALLOWED
	LDA I,0B9
	STA X,MOBJC
	LDA I,48		;ROW ABOUT NEW HEADS
	EOR CKF8
	STA X,MOBJV		;POSITION BEETLE
	LDA I,0
	STA X,MOBJDV
	STA X,MOBJH
	LDA I,1
	BIT RND1
	BMI 25$			;PICK A RANDOM DIRECTION
	LDA I,-1
25$:	STA X,MOBJDH		;SET SPEED AND DIRECTION
	LDA I,34
	STA X,MOBJP		;SET PICTURE CODE
	LDA I,0C
	STA X,MOBJHL		;SET DELAY TIMER
	LDY PLAYR
	LDA OPTNS1
	LSR
	LSR
	LDA I,39
	BCC 30$			;IF EASY BEETLES WAIT TIL 400,000
	LDA I,29		;ELSE FAST BEETLES AT 300,000
30$:	CMP Y,SCORE2
	BCS 50$			;SLOW BEETLES
	ASL X,MOBJDH		;FAST BEETLE
50$:	LDA BEETLS
	BEQ 95$			;NO BEETLES
	LDA CHAN0
	BNE 55$			;SOUND IS NOT FINISHED
	LDA I,2
	STA CHAN0		;START SOUND
55$:	LDX I,NCENT-1
60$:	LDA X,MOBJC
	BEQ 95$			;IF DEAD BEETLE
	LDA X,MOBJP
	CMP I,34
	BCC 95$			;NOT A BEETLE
	CMP I,38
	BCS 95$			;NOT A BEETLE
	ADC I,01
	AND I,03
	ORA I,34
	TAY
	LDA FRAME
	AND I,3
	BNE 65$			;UPDATE INFREQUENTLY
	STY X,MOBJP		;NEW PICTURE
65$:	LDA X,MOBJDV
	BEQ 70$			;DO HORIZONTAL ONLY
	CLC
	ADC X,MOBJV
	STA X,MOBJV
	JMP 80$

70$:	LDA X,MOBJDH
	CLC
	ADC X,MOBJH
	STA X,MOBJH		;NEW HORIZONTAL POSITION
	BNE 80$			;CLEAR BEETLE WHEN IT GOES OFF THE SCREEN
	STA X,MOBJC
	DEC BEETLS		;REDUCE COUNT OF BEETLES
	JMP 95$			;NO COLLISION POSSIBLE

80$:	JSR BEETL1		;CHECK FOR CHANGE IN DIRECTION
	LDA X,MOBJH
	STA TEMP1
	LDA X,MOBJV
	LDY I,0
	JSR OBSTAC		;CHECK FOR OBSTACLES
	CMP I,CLOUD
	BCC 90$			;NOT A DDT EXPLOSION
	CMP I,DDT
	BCS 85$			;NOT A DDT EXPLOSION
	LDA CPYRHT+12
	CMP I,49
	BEQ 82$			;IF "I" OF COPYRIGHT IS STILL THERE
	LDA I,8
SECUR3	=.
	STA $$CRDT		;ADD CREDITS
82$:	JSR DDTEX1		;PROCESS DEATH SEQUENCE
	BCS 95$			;ALWAYS

85$:	CMP I,75		;ALSO ALLOW GROWING AND POISONED MUSHROOMS
	BCC 90$			;NOT A MUSHROOM
	LDA NY,OBST
	AND I,80		;LEAVE BACKGROUND FOR PLAYERS AREA
	ORA I,ROCK
	STA NY,OBST		;TURN IT INTO A ROCK
90$:	JSR PLAY		;CHECK FOR PLAYER COLLISION
	BCC 99$			;IF PLAYER COLLISION
95$:	DEX
	BPL 60$			;CONTINUE THRU ALL ENTRIES
99$:	RTS


SECUR4:	.REPT 8-.&07		;ALLIGN ON 8 BYTE BOUNDARY
	.BYTE 0FF
	.ENDR
	.INCLUD MLCPY		;<<<<<<<<<<<<<<<<<<<<<<<<


BEETL1:	LDA X,MOBJDV
	ORA X,MOBJDH
	LSR
	BCS 5$			;IF A SLOW BEETLE
	DEC X,MOBJHL		;YES I KNOW THIS MUST BE EVEN
5$:	DEC X,MOBJHL
	BEQ 10$			;TIME FOR DIRECTION CHANGE
	RTS

10$:	LDA X,MOBJDV
	BNE 30$			;IF GOING VERTICAL
	LDA X,MOBJDH
	LSR
	LDA CKFF
	ORA I,1			;1 FOR UPRIGHT FF FOR COCKTAIL
	BCS 15$			;SLOW BEETLE
	ASL			;FAST BEETLE
15$:	LDY X,MOBJV
	CPY I,0F0
	BCS 20$			;IF ON TOP ROW (COCKTAIL)
	CPY I,09
	BCC 20$			;IF ON BOTTOM ROW
	JSR COMP		;COMPLEMENT DIRECTION
	STA X,MOBJDV		;CONTINUE TO BOTTOM ROW
	LDA I,40
	STA X,MOBJHL		;RESET TIMER
	RTS

20$:	STA X,MOBJDV
	LDA RND0
	AND I,38
	CLC
	ADC I,40
	STA X,MOBJHL		;RESET TIMER TO GO UP
	RTS

30$:	LDA I,0
	STA X,MOBJDV		;NO VERTICAL DIRECTION
	LDA X,MOBJV
	CMP I,0F0
	BCS 40$			;IF ON TOP ROW (COCKTAIL)
	CMP I,09
	BCC 40$			;IF ON BOTTOM ROW
	DEC X,MOBJHL		;SET TO FF TO GET OFF THE SCREEN
	RTS

40$:	LDA RND0
	AND I,78
	CLC
	ADC I,60
	STA X,MOBJHL		;RESET TIMER TO GO AT LEAST HALF WAY ACROSS THE BOTTOM
	RTS


	.SBTTL	BOMBS - BOMB PLAYER WITH BEE, MOSQUITO AND DRAGONFLY
;
; ENTRY	(NOCENT)=NUMBER OF CRITTERS TO ENTER
BOMBS:	LDA NOCENT
	BEQ 15$			;NO CRITTERS TO ENTER
	BIT MODE
	BMI 15$			;SPECIAL ATTRACT MODE NO BOMBING
5$:	LDA RND0
	AND I,7
	BNE 15$			;NOT AT THIS TIME
	LDA I,80
	STA DELAY		;WAIT UNTIL THEY REACH THE BOTTOM
	LDX I,12.
10$:	LDA X,BEEC
	BEQ 20$			;WE FOUND AN ENTRY
	DEX
	BPL 10$			;LOOK FOR AN ENTRY
15$:	RTS

20$:	LDY PLAYR
	LDA Y,CENTIN
	LSR
	TAY
	LDA Y,90$
	BMI 30$			;THESE ARE TO BE DRAGONFLIES
25$:	JSR BEEMV3		;ENTER A BEE
	DEC NOCENT		;JUST SO SPEED IS AT LEAST 2
	RTS			;AND LAST CRITTER GETS TO BOTTOM BEFORE DELAY=0

30$:	ASL
	BMI 40$			;ADD MOSQUITOS ONLY
	BEQ 35$			;DRAGONFLIES ONLY
	AND RND0
	BEQ 25$			;ADD A BEE
	CMP I,6
	BEQ 40$			;ADD A MOSQUITO
35$:	JSR FLYMV3		;ENTER A DRAGONFLY
	DEC NOCENT		;JUST SO SPEED IS AT LEAST 2
	RTS			;AND LAST CRITTER GETS TO BOTTOM BEFORE DELAY=0

40$:	JSR MOSQT3		;ENTER A MOSQUITO
	DEC NOCENT		;JUST SO SPEED IS AT LEAST 2
	RTS			;AND LAST CRITTER GETS TO BOTTOM BEFORE DELAY=0

90$:	.BYTE 83,81,0C0,80	;WHAT CREATURE TO ENTER (D7=1=DRAGONFLIES) (D7=D6=1=MOSQUITOS
	.BYTE 80,0		;(D7=0=BEES ONLY) (D7=1,D6=0,D0-D1=MULTIPLE CRITTERS)
BOMBSL:	.BYTE 1,3,5,7		;AFTER WHAT "CENTIN" TO ENTER CRITTERS
	.BYTE 0B

SECUR2:	.BYTE 2,0BB,5A,30	;0=DOT, 1=DASH
	.BYTE 5F,0EE,1D,0A8	;MORSE CODE FOR "COPYRIGHT 1982 ATARI"
	.PAGE
	.SBTTL	CENTPC-INITIALIZE CENTIPEDE PICTURE
;CENTPC-INITIALIZE CENTIPEDE PICTURE,HPOS,VPOS, & DIRECTION
CENTPC:	LDX PLAYR
	LDA X,DEAD  
	BNE 2$			;NO CHANGE IN COLOR OR SPEED UNTIL ALL DEAD
	DEC SCROLC		;SCROLL PLAYFIELD DOWN ONE ROW
2$:	LDA X,CENTIS  
	CMP I,03
	BCC 5$			;IF CENTIS < 3
	LDA I,0
	STA X,HITDDT		;CLEAR FLAG WHEN NEW WAVE STARTS
	DEC X,CENTIN  
	BNE 3$			;GO OR IF CENTIN>0
	LDA I,0C
	STA X,CENTIN  		;LONG CENTI AGAIN
3$:	INC LCOLOR		;CHANGE COLOR NOW
	LDA I,1
	LDY X,SCORE2  
	CPY I,2
	BCC 4$			;ONLY FAST CENTIPEDES AFTER 20000
	LDA I,2
4$:	STA X,CENTIS  
5$:	LDA I,03
	STA MOBJP		;HEAD PICTURE
	LDA X,CENTIS  
	BIT CKIND		;IF NOT COCKTAIL
	BPL 8$
	JSR COMP		;GO OPPOSITE DIRECTION
8$:	STA MOBJDV
	TAY
	BIT MODE
	BMI 9$			;IF SPECIAL ATTRACT MODE-MILLIPEDE STARTS TO THE RIGHT
	LDA RND0
	AND I,2
	BNE 10$
9$:	TYA
	JSR COMP
	TAY
10$:	STY MOBJDH		;HDIR=CENTIS*1 OR -1
	LDA I,0F8
	EOR CKF8		;0 OR F8
	STA MOBJV		;VPOS
	LDA I,80
	STA MOBJH		;HPOS
	LDA I,39
	STA MOBJC		;SET COLOR FOR HEAD
	LDA I,0
	STA PTS			;CLEAR ANY POINTS FROM ATTRACT
	LDY X,CENTIN
	LDA I,NCENT
	STA TEMP1+1		;MAX NUMBERS OF SEGMENTS
	BIT MODE
	BMI 15$			;IF SPECIAL ATTRACT SEQUENCE
	CPY I,0C
	BNE 15$			;IT'S NOT THE RIGHT TIME
	LDA X,SCORE2
	SEC
	SBC I,10
	BCC 15$			;SCORE IS TOO LOW
	LSR
	CMP I,8
	BCC 11$			;IF BELOW MAX OF 200,000
	LDA I,5
11$:	CLC
	ADC I,3			;NUMBER OF SPIDERS
	STA TEMP1
	LDA I,NCENT+2
	SEC
	SBC TEMP1		;NUMBER OF CENTIPEDE SEGMENTS
	TAY
	STY TEMP1+1		;MAX NUMBER OF SEGMENTS
	LDX I,NCENT
	LDA I,0
12$:	DEX
	STA X,PTS		;CLEAR ANY POINT TIMER
	STA X,MOBJC		;REMOVE EXTRA OBJECTS
	STA X,MOBJH		;REMOVE FROM SCREEN
	CPX TEMP1+1
	BNE 12$
15$:	STY TEMP1
	CPY I,01
	BEQ 60$			;IF N=1 (NO BODY SEGMENTS)
	LDY I,2
	LDX I,01
20$:	STY X,MOBJP		;BODY PICTURE
	LDA I,0
	STA X,PTS		;CLEAR ANY POINT TIMER
	LDA I,0F8
	EOR CKF8		;0 OR F8
	STA X,MOBJV		;VPOS
	LDA X,MOBJDV-1  
	STA X,MOBJDV
	LDA X,MOBJDH-1
	STA X,MOBJDH		;DIRECTION
	BPL 30$			;IF POSITIVE
	LDA I,8
	BNE 40$			;ALWAYS

30$:	LDA I,-8
40$:	CLC
	ADC X,MOBJH-1
	STA X,MOBJH		;HPOS
	LDA I,3D
	STA X,MOBJC		;SET COLOR FOR BODY
	DEY
	BPL 50$			;UNDERFLOW FOR BODY PICTURE
	LDY I,7			;ALTERNATE BODY SEGMENTS
50$:	INX
	CPX TEMP1
	BCC 20$			;IF (X)<(CENTIN-1)
60$:	LDX TEMP1
70$:	CPX TEMP1+1
	BCS 90$			;IF NO MORE FREE ENTRIES
	LDA I,0
	STA X,PTS		;CLEAR ANY POINT TIMER
	LDA I,0F8		;VPOS=F8
	EOR CKF8		;0 OR F8
	STA X,MOBJV		;VPOS
	LDA I,0
	STA X,MOBJP
	LDA I,2
	ORA CKFE		;2 OR -2 DEPENDING ON COCKTAIL
	STA X,MOBJDV
	BIT RND0
	BPL 82$			;IF GOING POSITIVE
	JSR COMP		;TRY OTHER DIRECTION
82$:	STA X,MOBJDH		;DIRECTION
83$:	LDA RND0
	AND I,0F8
	CMP I,0F8
	BEQ 83$			;TOO FAR TO THE LEFT
	CMP I,10
	BCC 83$			;TOO FAR TO THE RIGHT
	STA X,MOBJH		;HPOS
	LDA I,39
	STA X,MOBJC		;SET COLOR FOR EXTRA HEADS
	INX
	BPL 70$			;ALWAYS

90$:	LDA TEMP1+1
	LDX PLAYR
	STA X,DEAD  
	LDY I,1
	LDA X,SCORE2
	CMP I,7
	BCC 95$			;ALLOW 1 BEETLE PER WAVE UNTIL 70000
	INY
	CMP I,14
	BCC 95$			;ALLOW 3 AFTER 140000
	INY
	CMP I,21
	BCC 95$			;ALLOW 4 AFTER 210000
	INY
	CMP I,40
	BCC 95$			;ONLY 4 BEETLES UNTIL 400,000
	CMP I,70
	BCS 93$			;255 BEETLES AFTER 700,000
	CMP I,50
	BCS 94$			;6 BEETLES AFTER 500,000
	LDA OPTNS1
	AND I,02
	BEQ 95$			;IF EASY WAIT UNTIL 500,000
	BNE 94$			;IF HARD THEN 6 BEETLES AFTER 400,000

93$:	LDY I,0FD
94$:	INY
	INY
95$:	STY BEETLA
	LDA I,0
	STA BOMBV		;CLEAR ANY BOMBING VALUE
	STA NEWD		;NEWD=0(NOT TIME FOR PUTTING IN NEW HEAD)
	STA BEETLS		;CLEAR OUT ANY BEETLES
	RTS
	.PAGE
	.SBTTL	EARWIG-MOVE AND START EARWIG
EARWIG:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BNE 2$			;IF PLAYER IS DEAD
	LDA BEEC+12.
	BEQ 3$			;IF VACANT POSITION
	LDA BEEP+12.
	CMP I,1E
	BCS 2$			;NOT A EARWIG
	CMP I,1C
	BCS 50$			;THIS IS A EARWIG MOVE HIM
2$:	RTS

3$:	LDA FRAME
	BNE 2$			;EARWIG STARTS EVERY 4 SECONDS
	LDX PLAYR
	LDA X,CENTIN  
	CMP I,11.
	BCS 2$			;WAIT UNTIL WAVES WHERE THE CENTIPEDE IS SMALL
	LDA RND0
	AND I,03
	BNE 2$			;RANDOMLY ENTER EARWIG
	LDA I,1C
	STA BEEP+12.		;SET EARWIG PICTURE
	LDA I,0B9
	STA BEEC+12.		;TURN ON COLORS
	LDA X,SCORE2  
	CMP I,2
	BCC 5$			;IF SCORE <20K, USE SLOW SPEED
	LDA RND0
	AND I,03
	BEQ 5$			;RANDOMLY USE SLOW EARWIG OVER 20K
	LDA I,2
	BIT RND0
	BPL 10$			;CHOOSE RANDOM DIRECTION
	LDA I,-2
	BNE 10$			;ALWAYS

5$:	LDA I,1
	BIT RND0
	BPL 10$			;CHOOSE RANDOM DIRECTION
	LDA I,-1
10$:	STA BEEDH+12.
	LDA I,0
	STA BEEH+12.		;START AT EDGE
	STA BEEDV+12.		;CLEAR VERTICAL DIRECTION
	LDA RND0
	AND I,78
	CLC
	ADC I,70
	EOR CKF8
	STA BEEV+12.		;SET VERTICAL POSITION IN UPPER HALF OF SCREEN
50$:	LDA CHAN9
	BNE 52$			;IF SOUND IS GOING
	LDA I,20.
	STA CHAN9		;CONTINUE SOUND
52$:	LDA BEEH+12.
	CLC
	ADC BEEDH+12.
	STA BEEH+12.		;NEW HORIZONTAL POSITION
	STA TEMP1		;FOR "OBSTAC" BELOW
	BNE 60$			;IF NOT OFF SCREEN
	LDX I,12.
	LDA I,0
	STA CHAN9		;STOP SOUND
	JMP BEEOFF		;RESET ENTRY TO ALLOW BEES

60$:	LDA FRAME
	AND I,03
	BNE 70$			;EVERY FEW FRAMES CHANGE PICTURE
	LDA BEEP+12.
	EOR I,01		;UPDATE PICTURE NUMBER
	STA BEEP+12.
70$:	LDY I,0			;MAKE MUSHROOMS CHANGE WHEN EARWIG IS ON TOP
	LDA BEEV+12.
	JSR OBSTAC		;CHECK FOR OBSTACLE
	CMP I,CLOUD
	BCC 90$			;NOT A DDT EXPLOSION
	CMP I,DDT
	BCS 80$			;NOT A DDT EXPLOSION
	LDX I,12.
	JMP DDTEX1		;PROCESS DEATH SEQUENCE

80$:	CMP I,7C
	BCC 90$			;NOT A MUSHROOM
	AND I,0FB
	STA NY,OBST		;POISON THE MUSHROOM
90$:	RTS


	.SBTTL	EXPLOD-EXPLODE CENTIPEDE SEGMENTS AND PLAYER
;
EXPLOD:	LDX I,15.		;EXPLODING MUSHROOMS USES PLAYER'S SLOT
10$:	LDA X,MOBJC
	BEQ 25$			;OBJECT IS NOT ALIVE
	LDY X,MOBJP
	CPY I,0FA
	BEQ 20$			;IF FINISHED EXPLODING
	BCS 15$			;IF AN EXPLOSION
	CPY I,28
	BCC 25$			;IF NOT A SCORE
	CPY I,34
	BCS 25$			;IF NOT A SCORE
	INC X,PTS		;INCREASE TIMER
	BNE 25$			;TIMER HAS NOT ELAPSED
	LDA I,0
	STA X,MOBJC		;REMOVE FROM THE SCREEN
	STA X,MOBJH
	BEQ 25$			;ALWAYS

15$:	DEC X,MOBJP		;NEXT PICTURE
	BNE 25$			;ALWAYS

20$:	LDA X,PTS
	BEQ 22$			;IF NO POINTS TO BE DISPLAYED
	LDA CKFF
	STA X,MOBJDH		;SO SCORES COME OUT CORRECT
	LDA X,PTS
	STA X,MOBJP		;PUT UP POINTS FOR KILLING CRITTER
	LDA I,0FF
	STA X,MOBJC		;SET COLOR SO "OVRLAP" DOES NOT DETECT COLLISION
	LDA I,0A0		;LEAVE SCORE OUT FOR AWHILE
	STA X,PTS		;SET DELAY TIMER
	BNE 25$			;ALWAYS

22$:	LDA I,0
	STA X,MOBJC
	STA X,MOBJH
25$:	DEX
	BPL 10$
	LDX PEXPLD
	BEQ 70$			;NO PLAYER EXPLOSION
	DEC PEXPLD
	BEQ 60$			;IF END OF EXPLOSION
	TXA
	LSR
	CPX I,60-10
	BCC 40$			;IF NOT FLASHING COLORS
	TAY
	LDA Y,30$-<60-10/2>
	STA BKGND		;SET BACKGROUND COLOR
	LDA I,80+14
	STA CHAN10		;RESTART THE SOUND
	BNE 70$			;ALWAYS

40$:	LSR
	CPX I,60-40
	BCC 50$			;IF COLLAPSING PLAYER
	TAY
	LDA Y,32$-<60-40/4>
	STA PLAYC		;SPARKLE PLAYER
	LDA I,80+14		;RESET SOUND TO START
	CPX I,60-40
	BNE 45$
	LDA I,10
45$:	STA CHAN10		;REPEAT SOUND
	BNE 70$			;ALWAYS

50$:	ORA I,20
	STA PLAYP		;PLAYER DYING SEQUENCE
	BNE 70$			;ALWAYS

60$:	LDA I,0
	STA MEM
	STA PLAYP		;DO NOT LEAVE PICTURE AS A SCORE
	STA PLAYH		;MOVE PLAYER OFF THE SCREEN
	LDA I,PLYFLD/100
	STA MEM+1		;RESTORE MUSHROOMS
70$:	LDA FRAME
	AND I,7
	BNE 89$			;DO NOT UPDATE EXPLOSIONS YET
	JSR DDTS1		;SET UP PARAMETERS
75$:	LDA X,DDTADD-1
	STA OBST
	LDA X,DDTADD
	CMP I,PLYFLD+400/100
	BCC 86$			;NO EXPLOSION
	PHA
	SBC I,10		;CARRY WAS SET
	STA X,DDTADD		;NEXT STATE OF EXPLOSION
	AND I,3FF/100
	ORA I,PLYFLD/100
	STA OBST+1
	PLA
	AND I,0F0		;GET EXPLOSION INDEX
	LSR
	LSR
	LSR
	LSR			;1 TO F
	SBC I,01
	BNE 78$			;IF NOT LAST ENTRY
	STA X,DDTADD		;CLEAR DDT ENTRY IF DONE
78$:	STX TEMP2+1		;SAVE X
	TAX
	LDA X,98$		;GET INDEX INTO TABLES
	TAX
80$:	LDA X,90$
	CMP I,0FF
	BEQ 85$			;IF END OF LIST
	BIT CKIND
	BPL 81$			;IF UPRIGHT
	LDA I,0E2
	SEC
	SBC X,90$		;INVERT POSTIONS
81$:	TAY
	LDA NY,OBST		;GET PLAYFIELD STAMP
	AND I,7F
	BEQ 82$			;BLANKS ARE OK
	CMP I,CLOUD
	BCC 84$			;IF A LETTER
	CMP I,DDT
	BCC 82$			;CLOUDS CAN BE ERASED
	CMP I,DDT+2
	BCC 84$			;DO NOT DESTROY LETTERS OR DDT BOMBS
	STX TEMP1
	TYA
	CLC
	ADC OBST
	JSR MUSHD1		;DECREMENT MUSHROOM COUNT
	LDX TEMP1
82$:	LDA NY,OBST
	AND I,80		;SAVE GREY IN PLAYERS AREA
	ORA X,99$		;GET NEW STAMP
	STA NY,OBST
84$:	INX
	BNE 80$			;ALWAYS (I HOPE)

85$:	LDX TEMP2+1
86$:	DEX
	DEX
	DEC TEMP2
	BPL 75$			;TRY NEXT ENTRY
89$:	RTS

98$:	.BYTE 90$-90$		;INDEX INTO TABLES BELOW
	.BYTE 91$-90$
	.BYTE 92$-90$
	.BYTE 93$-90$
	.BYTE 94$-90$
	.BYTE 93$-90$
	.BYTE 94$-90$
	.BYTE 95$-90$
	.BYTE 96$-90$
	.BYTE 97$-90$
90$:	.BYTE 41		;OFFSETS FROM "DDTADD" TO CHANGE
	.BYTE 60,61,62		;THIS LIST CANNOT EXCEED 255 BYTES
	.BYTE 80,81,82
	.BYTE 0A1
	.BYTE -1		;-1=END OF LIST
91$:	.BYTE 40,42
	.BYTE 0A0,0A2,0C1
97$:	.BYTE 41
	.BYTE 60,61,62
	.BYTE 80,81,82
	.BYTE 0A1
	.BYTE -1
92$:	.BYTE 20,21,0C2
96$:	.BYTE 40,41,42
	.BYTE 60,61,62
	.BYTE 80,81,82
	.BYTE 0A0,0A1,0A2
	.BYTE 0C1
	.BYTE -1
93$:	.BYTE 0,1,2,22
	.BYTE 0C0,0E0,0E1,0E2
95$:	.BYTE 20,21
	.BYTE 40,41,42
	.BYTE 60,61,62
	.BYTE 80,81,82
	.BYTE 0A0,0A1,0A2
	.BYTE 0C1,0C2
	.BYTE -1
94$:	.BYTE 0,1,2
	.BYTE 20,21,22
	.BYTE 40,41,42
	.BYTE 60,61,62
	.BYTE 80,81,82
	.BYTE 0A0,0A1,0A2
	.BYTE 0C0,0C1,0C2
	.BYTE 0E0,0E1,0E2
	.BYTE -1
99$:	.BYTE 0			;90$
	.BYTE 0,0,0
	.BYTE 0,0,0
	.BYTE 0
	.BYTE 0			;NOT USED
	.BYTE 0,0		;91$
	.BYTE 0,0,0
	.BYTE 30		;97$
	.BYTE 34,31,2E
	.BYTE 35,32,2F
	.BYTE 33
	.BYTE 0			;NOT USED
	.BYTE 0,0,0		;92$
	.BYTE 3F,3A,36		;96$
	.BYTE 40,3B,37
	.BYTE 4A,3C,38
	.BYTE 42,3D,39
	.BYTE 3E
	.BYTE 0			;NOT USED
	.BYTE 0,0,0,0		;93$
	.BYTE 0,0,0,0
	.BYTE 4E,48		;95$
	.BYTE 4F,49,43
	.BYTE 50,4A,44
	.BYTE 51,4B,45
	.BYTE 52,4C,46
	.BYTE 4D,47
	.BYTE 0			;NOT USED
	.BYTE 63,5B,53		;94$
	.BYTE 64,5C,54
	.BYTE 65,5D,55
	.BYTE 66,5E,56
	.BYTE 67,5F,57
	.BYTE 68,60,58
	.BYTE 69,61,59
	.BYTE 42,62,5A
30$:	.BYTE 0D6,1F,0FF,1F	;BACKGROUND COLORS
	.BYTE 0FF,1F,0FF,1F
32$:	.BYTE 0FF,0FB,0FE,0FD	;FLASHING PLAYER COLORS
	.BYTE 0EF,0DF,0F6,0ED
	.BYTE 0DE,0F9,0E7,0D7


SECUR6:	.BYTE 6F,5F,0E0,0DF	;"PROGRAMMED BY ED LOGG" IN MORSE CODE
	.BYTE 5F,40,0C0,0C0	;0=DOT
	.BYTE 7F,9F,8F,0B0	;1=DASH
	.BYTE 7F,9F,4F,0E0	;FILL REST OF WORD WITH OPPOSITE OF LAST BIT
	.BYTE 0DF,0DF


	.SBTTL	FLYMV - ENTER AND MOVE DRAGONFLY
FLYMV:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BEQ 5$			;IF PLAYER IS ALIVE
	RTS

5$:	LDA BEEC+12.
	BNE 20$			;THIS IS NOT AN AVAILABLE ENTRY
	LDX PLAYR
	LDA X,DEAD
	BNE 12$			;IF CENTIPEDE IS NOT DEAD
	LDA BEETLS
	BNE 18$			;IF BEETLE IS STILL ON THE SCREEN
12$:	JSR BEEMV1		;GET NUMBER OF MUSHROOMS NEEDED
	ADC I,30		;CANNOT EXCEED 61
	CMP X,MUSH+2
	BCC 20$			;TOO MANY MUSHROOMS AT THE TOP
18$:	LDA X,CENTIN
	CMP I,10.
	BCS 20$			;NO FLIES IF CENTIPEDE IS NEARLY WHOLE
	LDX I,12.
	JSR FLYMV3		;START A FLY HERE
20$:	LDX I,12.
	LDA I,0
	STA TEMP1+1		;INITIALIZE FOR NO VOLUME
30$:	LDA X,BEEC
	BEQ 50$			;NO OBJECT HERE
	LDA X,BEEP
	CMP I,20
	BCS 50$			;NOT A DRAGONFLY
	CMP I,1E
	BCC 50$			;NOT A DRAGONFLY
	EOR I,01
	TAY
	LDA SLOW
	BNE 31$			;IF IN SLOW DOWN MODE
	LDA FRAME
	AND I,01
	BEQ 32$			;LESS FREQUENT UPDATES
31$:	STY X,BEEP		;CHANGE PICTURE
32$:	JSR FLYMV1		;MOVE HORIZONTALLY
	LDA TEMP2+1
	BPL 40$			;IF POSITIVE
	EOR I,0FF
40$:	LSR
	LSR
	LSR
	LSR			;0 TO 7
	CLC
	ADC I,0A3		;A3 TO AB
	LDY NOCENT
	BEQ 45$			;NOT IN BOMBING MODE
	ADC I,2			;MORE VOLUME
45$:	STA TEMP1+1
	LDA I,01
	STA CHAN4		;TURN ON DRAGONFLY SOUND
	JSR BEEMV0		;MOVE VERTICAL AND ADD MUSHROOMS LIKE A BEE
	BCC 70$			;IF PLAYER HAS BEEN HIT
50$:	DEX
	BPL 30$			;TRY NEXT ENTRY
	LDA MODE
	BMI 70$			;IF IN ATTRACT
	LDA TEMP1+1
	BEQ 70$			;IF NO FLIES
	STA AUDC1		;SET AMPLITUDE
	LDA TEMP2+1
	LSR
	LSR
	LSR
	ORA I,90
	STA AUDF1		;SET FREQUENCY TOO
70$:	RTS


;
;	ENTRY (X)=MOTION OBJECT INDEX
;	EXIT (TEMP2+1)=HORIZONTAL OFFSET FOR AUDIO (-80 TO +7F)
;	USES TEMP1,(TEMP2,TEMP2+1)
FLYMV1:	LDA I,3F
	AND FRAME
	ASL
	ASL
	CMP I,40
	BCC 35$			;IF NOT TIME TO CHANGE SIGN
	CMP I,0C0
	BCS 35$			;IF NOT TIME TO CHANGE SIGN
	EOR I,7F		;CHANGE SIGN
35$:	EOR X,BEEDH		;BOUNCE OFF WALLS
	LDY X,BEEH
	CPY I,0E
	BCS 36$			;NOT ON THE RIGHT EDGE
	TAY
	BPL 38$			;GOING TO THE LEFT
	BMI 37$			;CHANGE DIRECTION

36$:	CPY I,0F4
	BCC 38$			;NOT ON THE LEFT EDGE
	TAY
	BMI 38$			;GOING TO THE RIGHT
37$:	LDA X,BEEDH
	EOR I,0FF
	STA X,BEEDH		;CHANGE DIRECTION
	TYA
	JSR COMP		;BOUNCE OFF THE EDGE
38$:	LDY I,0
	ASL
	BPL 46$			;SIGN EXTEND
	DEY
46$:	STY TEMP2
	STA TEMP2+1		;-80 TO +7F
	ASL
	ROL TEMP2
	ASL
	ROL TEMP2
	STX TEMP1
	LDX PLAYR
	LDY X,SCORE2
	BIT MODE
	BPL 47$			;SPECIAL ATTRACT MODE SCORE=0
	LDY #00
47$:	LDX TEMP1
	CPY I,3
	BCC 48$			;LESS VIOLET MOTION UP TO 30000
	ASL
	ROL TEMP2		;IN THE RANGE 2 TO -2
48$:	CLC
	ADC X,BEEHL
	STA X,BEEHL
	LDA TEMP2
	ADC X,BEEH
	STA X,BEEH
	RTS


FLYMV3:	LDA I,1E
	STA X,BEEP
	JSR BEEMV2		;INITIALIZE PARAMETERS
	LDA I,3
	STX TEMP1
	LDX PLAYR
	LDY X,SCORE2
	CPY I,15
	BCS 18$			;AFTER 150,000-VERY FAST
	LDA I,2
	CPY I,5
	BCS 18$			;DRAGONFLY GOES FAST AFTER 50000
	LDY NOCENT
	BNE 18$			;IN BOMBING MODE
	LDA I,1
18$:	LDX TEMP1
	BIT CKIND
	BPL 19$			;IF NOT COCKTAIL
	JSR COMP		;REVERSE DIRECTIONS
19$:	STA X,BEEDV
	RTS
	.PAGE
	.SBTTL	INIT-INITIALIZE EVERYTHING
;INIT-INITIALIZE EVERYTHING
;
INIT:	LDA I,0
	STA ACTL0
	LDA I,20		;SHOT SOUND ONLY
	STA ACTL1		;CLOCK CHAN3 WITH 1.5 MHZ
	LDA I,12.
	STA CENTIN
	STA CENTIN+1		;SET CENTIPEDE SIZE
	LDA I,02		;FAST TO START WITH
	STA CENTIS
	STA CENTIS+1		;SET CENTIPEDE SPEED
	LDX I,NCHAN
	LDA I,0
	STA PLAYR		;SET PLAYER NUMBER
	STA SKCTL0		;INITIALIZE POKEY
	STA SKCTL1
5$:	STA X,CHAN		;INITIALIZE SOUNDS
	DEX
	BPL 5$
	LDX I,05
6$:	STA X,SCORE0
	DEX
	BPL 6$			;CLEAR ALL SCORES
	LDX I,NCENT
8$:	TXA 
	ASL
	ASL
	ASL
	STA X,COUNT2		;OFFSET SPIDERS SO THEY DO NOT ENTER AT ONCE
	DEX
	BNE 8$
	LDA I,7
	STA SKCTL0		;TURN ON POKEY
	STA SKCTL1
	JSR CENTPC		;INITIALIZE CENTIPEDE
	LDA I,0C0
	STA COUNT1		;INITIALIZE COUNTERS FOR ADDING NEW HEADS
	STA COUNT3
	STA COUNT3+1
	LDX I,13.
	JSR SPDOFF		;INITIALIZE SPIDER
	LDX I,12.
	JSR BEEOFF		;SAME FOR BEE
INITSC =.
	LDA I,0FF
	STA ANCOL		;BLACK BACKGROUND
	STA ANCOL+4
	LDA I,0D6
	STA BKGND		;SET GREY FOR PLAYER AREA
	INC LCOLOR		;MAKE SURE OF CORRECT STARTING COLOR
	LDX I,0
	TXA
10$:	STA AX,PLYFLD		;CLEAR PLAYFIELD
	STA AX,PLYFLD+100
	STA AX,PLYFLD+200
	STA AX,PLYFLD+300
	INX
	BNE 10$
	LDA MODE
	CMP I,1
	BEQ INIT1		;IF WE ARE SELECTING STARTING SCORE
	LDX PLAYR
	STA X,MUSH  		;CLEAR COUNT AFTER ATTRACT
	STA X,MUSH+2
	JSR DDTS		;ADD DDT BOMBS
				;ANOTHER ATTRACT MODE FEATURE
	LDA MODE
	BPL 20$
	JSR INITAT		;INITIALIZE MUSHROOMS FROM TABLE
	JMP 30$
				;END FEATURE
20$:	LDA I,1D
	LDY I,2
	LDX I,55.		;ENOUGH FOR 2 MUSHROOMS IN EACH ROW
	JSR INIT3		;ADD MUSHROOMS
30$:	JSR INIT2		;ADD GREY AREA
INIT1:	LDA I,0A
	STA PLAYP		;GUN
	LDA I,80
	STA PLAYH		;GUN HPOS
	STA SHOTH
	LDA I,8
	BIT CKIND
	BPL 10$			;IF NOT COCKTAIL
	LDA I,0F0
10$:	STA PLAYV
	STA SHOTV		;SHOT VPOS
	LDA I,0B
	STA SHOTP
	LDA I,0FF
	STA PLAYC		;SET COLOR FOR PLAYER AND SHOT
	STA SHOTC
	LDA I,1
	STA PLAYHL		;DIRECTION FOR ATTRACT
	STA PLAYDV
	LDA I,0
	STA TB
	STA TB+1		;CLEAR ANY OLD TRACKBALL VALUES
	RTS



INIT2:	LDY I,PLYFLD+3A6&0FF
	LDA I,PLYFLD+3A6/100
	BIT CKIND
	BPL 30$			;NOT IN COCKTAIL
	LDY I,PLYFLD+3BE&0FF
	LDA I,PLYFLD+3BE/100
30$:	STA OBST+1
	LDA I,0
	STA OBST
40$:	LDX I,06
45$:	LDA NY,OBST		;SET THE PLAYERS AREA TO GREY
	EOR I,80
	STA NY,OBST
	DEY
	DEX
	BNE 45$
	TYA
	SEC
	SBC I,26.
	TAY
	BCS 40$			;NO UNDERFLOW
	DEC OBST+1
	LDA OBST+1
	CMP I,PLYFLD/100
	BCS 40$			;STOP AT END OF PLAYFIELD
	RTS


INIT3:	STA TEMP2+1		;TOP ROW
	STA TEMP1
	STY TEMP2		;BOTTOM ROW
20$:	LDA RND0		;RANDOM NUMBER
	AND I,0E0
	ORA TEMP1
	TAY
	LDA RND0		;RANDOM NUMBER
	AND I,03
	CMP I,3
	BCC 25$			;IF NOT OFF THE SCREEN
	CPY I,0C0
	BCS 40$			;IF OFF THE SCREEN
25$:	ORA I,PLYFLD/100
	STA OBST+1
	STY OBST
	STX TEMP3
	JSR MUSHER		;ADD MUSHROOM AND COUNT
	LDA TEMP1
	SEC
	SBC I,01
	CMP TEMP2
	BCS 35$			;STAY OFF BOTTOM ROW
	LDA TEMP2+1
35$:	STA TEMP1
	LDX TEMP3
40$:	DEX
	BNE 20$			;PUT UP OBSTACLES
	RTS


	.SBTTL	MOSQT-ENTER AND MOVE THE MOSQUITO
MOSQT:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BEQ 5$			;PLAYER IS ALIVE
	RTS

5$:	LDA BEEC+12.
	BNE 20$			;THIS ENTRY IS NOT AVAILABLE
	LDA FRAME
	LDX PLAYR
	LDY X,SCORE2
	CPY I,7
	BCC 8$			;IF LESS THAN 70,000 MAKE MOSQUITO LESS FREQUENT
	AND I,7F		;EVERY 2 SECONDS
8$:	EOR I,17		;ASYNRONOUS WITH REST OF PROGRAM
	BNE 20$			;DO NOT ENTER YET
	LDX PLAYR
	LDA X,CENTIN
	CMP I,9
	BCS 20$			;NO FLIES YET
	LDX I,12.
	JSR MOSQT3		;ENTER A MOSQUITO
20$:	LDX I,12.
30$:	LDA X,BEEC
	BEQ 50$			;NO OBJECT THERE
	LDA X,BEEP
	CMP I,10
	BCS 50$			;NOT A MOSQUITO
	CMP I,0E
	BCC 50$			;NOT A MOSQUITO
	EOR I,01
	TAY
	LDA SLOW
	BNE 31$			;IF IN SLOW DOWN MODE
	LDA FRAME
	AND I,01
	BEQ 32$			;NOT THIS FRAME
31$:	STY X,BEEP		;FLAP THE WINGS
32$:	LDA CHAN5
	CMP I,4
	BCS 34$			;IF SOUND IS ALREADY ON
	SEI			;DO NOT LET US MESS UP SOUND
	ADC I,4
	STA CHAN5		;UPDATE COUNTER
	CLI
34$:	LDA X,BEEDH
	CLC
	ADC X,BEEH
	STA X,BEEH
	CMP I,0C
	BCC 35$			;IF OFF THE SCREEN
	CMP I,0F5
	BCC 40$			;NOT OFF THE SCREEN
35$:	LDA X,BEEDH
	JSR COMP		;CHANGE DIRECTION
	STA X,BEEDH
40$:	LDA X,BEEV
	SEC
	SBC X,BEEDV
	STA X,BEEV
	EOR CKFF
	CMP I,4
	BCS 45$			;IF STILL ON THE SCREEN
	JSR BEEOFF		;CLEAR THIS OBJECT
	JMP 50$

45$:	LDA X,BEEH
	STA TEMP1
	LDA X,BEEV
	LDY I,0
	JSR OBSTAC		;GET PLAYFIELD ADDRESS
	JSR DDTEXP		;CHECK FOR DDT EXPLOSIONS
	BCS 50$			;IF MOSQUITO JUST DIED
	JSR PLAY		;CHECK PLAYER COLLISION
	BCC 70$			;IF PLAYER JUST DIED
50$:	DEX
	BPL 30$
70$:	RTS


MOSQT3:	LDA I,0E
	STA X,BEEP		;SET PICTURE
	JSR BEEMV2		;INITIALIZE PARAMETERS
	LDA I,36.
	STA CHAN5		;START SOUND
	LDA I,2
	STX TEMP1
	LDX PLAYR
	LDY X,SCORE2
	LDX TEMP1
	CPY I,9
	BCC 20$			;IF OVER 90000 THEN USE FAST MOSQUITO
	LDA $CMODE
	AND I,0C3
	CMP I,0C0
	BEQ 10$			;IF IN DEMO MODE
	LDA CPYRHT+0E		;"A" FROM ATARI
	CMP I,41
	BEQ 10$			;IF STILL IN ROM
	STA 1FE			;KILL THE RTS
SECURA	=.-1
10$:	LDA I,3
20$:	BIT CKIND
	BPL 25$			;IF NOT UPSIDE DOWN
	JSR COMP		;REVERSE DIRECTION
25$:	STA X,BEEDV
	BIT RND1
	BPL 30$			;CHOOSE ONE DIRECTION OR THE OTHER
	JSR COMP
30$:	STA X,BEEDH
	RTS

SECUR7:	.BYTE 80,40,20,0C0	;"ENGINEERED BY DOUG SNYDER" IN MORSE CODE
	.BYTE 40,80,80,0A0	;1=DOT 0=DASH
	.BYTE 80,60,70,4F	;FILL REST OF WORD WITH OPPOSITE OF LAST VALUE
	.BYTE 60,1F,0DF,20
	.BYTE 0E0,40,4F,60
	.BYTE 80,0A0
.SBTTL	MOTION-MOTION UPDATE
;MOTION-UPDATE POSITION OF MOTION OBJECTS
;
MOTION:	LDA DELAY
	BNE 2$			;IF DELAY
	LDX I,NCENT-1  
	LDA CHAN1	
	BNE 4$			;IF NOT 1/4 SECOND BOUNDARY
	LDA I,20
	STA CHAN1		;START LITTLE FEET NOISE
4$:	LDA X,MOBJC
	BEQ 5$			;IF EMPTY ENTRY
	LDA X,MOBJP
	CMP I,08
	BCC 6$			;IF A CENTIPEDE			
5$:	DEX
	BPL 4$			;IF NOT END OF ENTRIES
2$:	RTS

6$:	LDA FRAME
	AND I,1
	BNE 7$			;SLOW DOWN LEGS
	LDA X,MOBJP
	CLC
	ADC I,01
	AND I,7
	STA X,MOBJP		;MOVE LEGS
7$:	LDY I,1
	LDA X,MOBJV
	EOR CKF8
	CMP I,9
	BCS 71$			;IF NOT ON BOTTOM ROW
	LDA X,MOBJC
	CMP I,39
	BNE 70$			;IF BODY OR POISONED HEAD
	STY NEWD		;START ADDING NEW HEADS
70$:	LDA X,MOBJV		;RESTORE A
71$:	AND I,07
	BNE 15$			;DON'T SPEED UP IF TURNING
	TYA
	LDY PLAYR
	CMP Y,DEAD  
	BNE 78$			;IF MORE THAN 1 LIVE SEGMENT
	LDA I,2
	LDY X,MOBJDH
	BPL 72$
	LDA I,-2
72$:	STA X,MOBJDH		;SPEED UP LAST HEAD
	LDA I,2
	LDY X,MOBJDV
	BPL 73$
	LDA I,-2
73$:	STA X,MOBJDV		;SPEED UP LAST HEAD
78$:	LDA X,MOBJC
	CMP I,3D
	BNE 10$			;CENTIPEDE HEAD OR POISONED HEAD
	LDA X,MOBJV-1  
	SEC
	SBC X,MOBJV		;/(PREV.SEG.VPOS)-(CUR.SEG.VPOS)/
	JSR ABS
	CMP I,08
	BCS 15$			;DIFFERENCE=8
8$:	JMP 20$
	
10$:	CMP I,1B
	BEQ 15$			;IF HEAD IS POISONED GO TO BOTTOM OF SCREEN
	LDA X,MOBJH
	CMP I,0F0
	BCC 11$			;IF NOT AT LEFT EDGE
	LDY X,MOBJDV
	BEQ 115$
	LDY X,MOBJDH
	BPL 15$			;CHANGE VPOS
	BMI 12$			;DO NOT CHANGE VPOS
	
11$:	CMP I,10
	BCS 12$			;IF NOT A RIGHT EDGE
	LDY X,MOBJDV
	BNE 117$
115$:	JMP 25$

117$:	LDY X,MOBJDH
	BMI 15$			;CHANGE VPOS
12$:	JSR OBSTA0
	BEQ 13$			;NO OBSTACLE
	CMP I,CLOUD
	BCC 15$			;TURN AT LETTER
	CMP I,DDT
	BCC 13$			;GO THRU DTT CLOUDS
	CMP I,78
	BCC 15$			;DDT OR DYING GROWING MUSHROOM
	CMP I,7C
	BCS 15$			;IF NOT A POISIONED MUSHROOM
	LDA I,1B
	STA X,MOBJC		;SET HEAD AS POISONED
	BCC 15$			;ALWAYS

13$:	JSR OVRLAP		;CHECK FOR OVERLAP
	BCC 8$			;NO OVERLAP OF SEGMENTS
15$:	LDA X,MOBJDV
	BEQ 115$		;IF DV=0 TURN AROUND
	EOR CKFF
	ASL			;SET CARRY
	LDA X,MOBJV
	EOR CKF8
	BCC 16$			;GOING DOWN SCREEN
	BIT CKIND
	BPL 155$		;NOT COCKTAIL NOW
	EOR CKF8
	CMP I,0C9
	BCC 167$		;TURN DOWN IN COCKTAIL
	BCS 18$			;ALWAYS

155$:	CMP I,30
	BCS 167$		;TURN DOWN
	BCC 18$			;ALWAYS

16$:	CMP I,9
	BCS 18$			;IF NOT AT BOTTOM
	LDA X,MOBJC
	CMP I,3D
	BEQ 167$		;IF NOT A HEAD
	LDA I,39
	STA X,MOBJC		;CLEAR POISONED BIT
	CPX I,NCENT-1
	BEQ 167$		;LAST CENTIPEDE ENTRY
	TXA
	TAY
	INY
	LDA Y,MOBJC
	CMP I,3D
	BNE 167$		;NO BODY TO THIS CENTIPEDE
163$:	CPY I,NCENT-1
	BEQ 164$		;NO MORE ENTRIES
	LDA Y,MOBJC+1
	CMP I,3D
	BEQ 165$		;IF NEXT SEGMENT HEAD
164$:	LDA Y,MOBJV		;TAIL MAY NOT BE ON THE SAME LINE
	CLC
	ADC I,04
	AND I,0F8
	STA Y,MOBJV		;BE SURE TAIL STARTS ON THE RIGHT LINE
	LDA Y,MOBJDH
	JSR COMP
	STA Y,MOBJDH		;REVERSE HDIR
	LDA I,39
	STA Y,MOBJC		;TURN ON COLOR FOR EYES
	LDA I,0			;DV=0 FOR ONE LINE
	STA Y,MOBJDV
	BEQ 167$		;ALWAYS

165$:	INY
	CPY I,NCENT
	BCC 163$		;GO THRU AL OBJECTS
167$:	LDA X,MOBJDV
	JSR COMP
	STA X,MOBJDV		;COMPLEMENT MOBJDV
18$:	LDA X,MOBJV
	SEC
	SBC X,MOBJDV
	STA X,MOBJV		;CHANGE VPOS
20$:	LDA X,MOBJDH
	CLC
	ADC X,MOBJH
	STA X,MOBJH		;CHANGE HPOS
	STA TEMP1
	LDA X,MOBJV
	LDY I,0
	JSR OBSTAC		;GET PLAYFIELD POSITION
	JSR DDTEXP		;CHECK FOR DDT CLOUD COLLISION
	BCS 30$			;OBJECT DESTROYED
	JSR PLAY		;SEGMENT AND PLAYER COLLISION?
	BCC 30$			;IF PLAYER IS DEAD
	LDA X,MOBJV
	AND I,07
	CMP I,04
	BNE 30$			;NOT TIME TO CHANGE DIRECTION
25$:	LDA X,MOBJDH
	JSR COMP
	STA X,MOBJDH		;CHANGE DIRECTION
	LDY X,MOBJDV		;A TAIL WHICH JUST SPLIT SHOULD NOT TURN
	BNE 30$			;IF VDIR IS NOT ZERO
	EOR CKFF
	ASL
	LDA X,MOBJDH
	BCS 27$			;USE NEGATIVE HDIR=VDIR
	JSR COMP
27$:	STA X,MOBJDV		;SET VDIR=HDIR
30$:	JMP 5$

SECUR8:	.BYTE 0D4,0C9,0D3,0D3	;"MILLIPEDE BY ATARI" IN 8 BIT EBCDIC CODE
	.BYTE 0C9,0D7,0C5,0C4	;A=C1, B=C2,..., I=C9
	.BYTE 0C5,40,0C2,0E8	;J=D1, ...	,R=D9
	.BYTE 40,0C1,0E3,0C1	;S=E2, ...	,Z=E9
	.BYTE 0D9,0C9
	.PAGE
.SBTTL	MOVE-MOVE PLAYER
;MOVE-MOVE PLAYER
;
MOVE:	LDA MODE
	BPL 2$			;IF NOT ATTRACT
1$:	RTS

2$:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BNE 1$			;HE'S DEAD JIM, DON'T MOVE HIM (THE PLAYER)
202$:	LDA PLAYV
	STA TEMP2		;SAVE OLD VERTICAL POSITION
	LDY I,0			;IT BETTER BE ZERO
	LDA TB			;HORIZONTAL CHANGE IN TRACKBALL
	STY TB			;CLEAR FOR NEXT FRAME
	JSR TBLMT
	ADC PLAYHL
	STA PLAYHL		;KEEP FINER RESOLUTION
	TYA
MHOR	=.
205$:	ADC PLAYH
	TAX
	STA TEMP1		;OBSTAC CALLS
	LDY I,0
	LDA PLAYV
	JSR OBSTAC
	BNE 5$			;OBSTACLE ENCOUNTERED
	TXA
	CMP I,0F4
	BCC 3$			;IF NOT AT LEFT EDGE
	LDA I,0F4		;KEEP AT EDGE
	BNE 10$			;ALWAYS

3$:	CMP I,0B
	BCS 10$			;IF NOT AT RIGHT EDGE
	LDA I,0B		;KEEP AT EDGE
	BNE 10$			;ALWAYS

5$:	LDA PLAYH
10$:	STA PLAYH		;UPDATE HPOS
	LDY MODE
	BPL 20$			;IF NOT IN ATTRACT
	RTS
	
20$:	LDY I,0
	LDA TB+1
	STY TB+1		;CLEAR FOR NEXT FRAME
	JSR COMP		;DIRECTIONS ARE REVERSED RELATIVE TO MOTION OBJECT VIDEO
	JSR TBLMT
	ADC PLAYVL		;KEEP LSB FOR VERTICAL DIRECTION
	STA PLAYVL
	TYA
MVER	=.
35$:	ADC PLAYV
	TAX
	LDY PLAYH
	STY TEMP1
	LDY I,0
	JSR OBSTAC
	BNE 45$			;OBSTACLE ENCOUNTERED
	TXA
	CMP I,8
	BCC 43$			;BELOW BOTTOM BOUNDARY
	CMP I,0F1
	BCS 40$			;BELOW BOTTOM BOUNDARY FOR COCKTAIL
	CMP I,80
	BCC 37$			;NOT COCKTAIL NOW
	CMP I,0C8
	BCS 50$			;WITHIN TOP BOUNDARY
	LDA I,0C8
	BNE 50$			;ALWAYS

37$:	CMP I,31
	BCC 50$			;WITHIN TOP BOUNDARY
	LDA I,30
	BNE 50$			;ALWAYS

40$:	LDA I,0F0
	BNE 50$			;ALWAYS

43$:	LDA I,8			;KEEP AT BOTTOM EDGE
	BNE 50$			;ALWAYS

45$:	LDA PLAYV
50$:	STA PLAYV
	LDY MODE
	BPL 60$			;IF NOT IN ATTRACT
	RTS
	
RSHOT1	=.
60$:	LDA SHOTV
	CMP TEMP2
	BNE 70$			;SHOT IS GOING UP
RSHOT	=.
	LDA I,0FF
	STA SHOTC		;CLEAR FLAG FOR SHOT
	LDA PLAYV
	STA SHOTV
	LDA PLAYH
	STA SHOTH		;REPOSTION SHOT ON GUN
70$:	RTS


	.SBTTL	PLAY-CHECK FOR PLAYER COLLISION
;
;ENTRY	(X)=INDEX OF CURRENT OBJECT
;EXIT	(X)=INDEX OF CURRENT OBJECT
;	(CC)=CARRY CLEAR IF PLAYER HAS HAD A COLLISION
;USES	A,Y,TEMP2
PLAY:	LDY I,0
	LDA X,SPDP
	CMP I,14
	BCC 5$			;IF NOT A SPIDER
	CMP I,1C
	BCS 5$			;IF NOT A SPIDER
	LDY I,80		;Y=NEGATIVE FOR SPIDER
5$:	LDA PLAYH
	SEC
	SBC X,MOBJH		;X=/(OBJECT HPOS)-(PLAYER HPOS)/
	EOR CKFF
	BPL 8$			;IF POSITIVE
	EOR I,0FF		;-10 BECOMES +9
8$:	INY
	BPL 10$			;IF NOT SPIDER
	CMP I,10.
	BCC 15$			;IF COLLISION POSSIBLE
25$:	RTS

10$:	CMP I,06
	BCS 25$			;DIFF >= 6
15$:	STA TEMP2
	LDA X,MOBJV
	SEC
	SBC PLAYV		;Y=/(OBJECT VPOS)-(PLAYER VPOS)/
	EOR CKFF
	BPL 20$			;IF POSITIVE
	EOR I,0FF		;-6 NOW IS +5
20$:	CMP I,6
	BCS 25$			;DIFF>=6
	INY
	BPL 30$			;IF NOT A SPIDER
	ASL			;2*V
30$:	CLC
	ADC TEMP2		;H+V OR H+2*V
	CMP I,0A
 	BCS 25$			;IF NOT A HIT
	LDA $CMODE
	AND I,0C3
	CMP I,0C0
	BNE 31$			;IF NOT IN DEMO MODE
	INY
	SEC
	BPL 25$			;ONLY SPIDER COLLISION IN DEMO MODE
31$:	LDA I,10
	STA DELAY		;SET DELAY BEFORE NEXT SETUP
	LDA SHOTV
	CMP PLAYV
	BNE 33$			;IF SHOT IS ALREADY FIRED
	LDA I,0
	STA SHOTH		;BLANK SHOT
33$:	LDA I,60
	STA PEXPLD		;EXPLOSION SOUND FOR PLAYER
	TXA
	LDX PLAYR
	INC X,HITDDT		;STOP SCROLLING IN AUTO-SCROLL MODE
	TAX
	LDA I,0			;NO OTHER SOUNDS
	STA BEETLS		;NO BEETLES ON THE SCREEN (SEE "CHKEND")
	STA NOCENT		;STOP DROPPING CREATURES ONTO PLAYER
	STA CHAN8		;STOP SLOW DOWN SOUND
	STA SLOW		;STOP SLOW DOWN MODE
	STA SCROLC		;STOP ANY EXISTING SCROLLING
	LDY I,NCHAN-2
35$:	STA Y,CHAN		;STOP SOUNDS EXCEPT
	DEY			;BONUS LIFE & PLAYER EXPLOSION
	BPL 35$
	CLC
	RTS			;CHANGE PICTURE TO EXPLOSION
	.PAGE
	.SBTTL	SHOOT-CHECK FIRE SWITCH AND FIRE SHOT
;SHOOT-CHECK FIRE SWITCH AND FIRE SHOT
;
SHOOT:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BEQ 1$			;IF PLAYER IS ALIVE
	RTS

1$:	LDA SHOTC
	CMP I,0FF
	BNE 3$			;REPOSITION SHOT-WE HIT SOMETHING LAST FRAME
	LDA SHOTV
	BIT CKIND
	BPL 2$			;NOT COCKTAIL NOW
	CLC
	ADC I,07
	EOR I,0FF
2$:	CMP I,0F2
	BCC 4$			;IF NOT OFF TOP OF SCREEN-RESET POSITION
3$:	JSR RSHOT		;REPOSITION SHOT
4$:	LDA PLAYV
	CMP SHOTV
	BNE 102$		;SHOT IS GOING UP
	LDA FIRE1		;D4=FIRE SWITCH
	LDX MODE
	BPL 6$			;GAME PLAY MODE
				;
	JSR SHOATT		;ATTRACT MODE SHOOTING
				;
6$:	BIT CKIND
	BPL 8$			;NOT COCKTAIL NOW
	LDA FIRE2
8$:	AND I,10
	BEQ 10$			;SWITCH JUST PUSHED DOWN
	BNE 30$			;FIRE SW OFF

10$:	LDA I,0B
	STA CHAN6		;SHOT SOUND
102$:	JSR SHOOT1		;CHECK FOR PLAYFIELD OBJECTS
	BNE 20$			;WE HIT SOMETHING
	LDA I,7
	EOR CKFE		;7 OR -7
	CLC
	ADC SHOTV
	STA SHOTV		;NEW SHOT POSITION
	LDX I,NCENT+1
115$:	LDA X,MOBJC
	BEQ 16$			;IF ENTRY IS NOT ALIVE
	LDA X,MOBJV
	EOR CKF8
	CMP I,0F4
	BCS 16$			;IF OFF TOP OF SCREEN
	EOR CKF8
	SEC
	SBC SHOTV
	JSR ABS			;ABSOLUTE
	CMP I,6			;SHOT UP 7, AND BEE DOWN 4 SO SHOT WILL NEVER MISS
	BCS 16$			;NOT IN V WINDOW
	LDA X,MOBJP
	AND I,3F
	CMP I,03A
	BCS 16$			;IF AN EXPLOSION
	LSR
	LSR
	TAY
	LDA X,MOBJH
	SEC
	SBC SHOTH
	JSR ABS			;ASSUMES SHOT IS IN MIDDLE OF EVERYTHING
	CMP Y,99$
	BCC 18$			;WE HAVE A COLLISION
16$:	DEX
	BPL 115$		;CONTINUE THRU ALL MOTION OBJECTS
	BMI 30$			;SHOT MISSED EVERYTHING

18$:	LDA I,0
	JSR DDTEX2		;PROCESS COLLISION RESULTS
20$:	LDA I,0C3
	STA SHOTC		;LEAVE SHOT IN CASE PLAYER DIES THIS FRAME
30$:	LDX PLAYR
	LDA X,DEAD  
	ORA DELAY
	BNE 60$			;IF ALL DEAD AND NO DELAY FOR NEXT WAVE
	LDA I,40
	STA DELAY		;DELAY BEFORE NEXT WAVE
	INC X,CENTIS  		;FASTER
	LDA X,CENTIS
	CMP I,03
	BCC 60$			;IF SLOW CENTIPEDE WAS JUST FINISHED
	BNE 60$			;THIS IS ATTRACT AND PLAYER DIED IN LAST BOMB MODE
	LDA X,CENTIN
	CMP I,9
	BNE 50$			;IF NOT TIME FOR CONWAY GROWTH/DEATH
	JMP INICON		;START CONWAY

50$:	LDY I,4
55$:	CMP Y,BOMBSL		;IS THIS THE LEVEL TO BOMB THE PLAYER
	BNE 59$			;NO
	LDA X,SCORE2
	LSR
	ADC I,20.		;MORE PER SCORE
	STA NOCENT
	INC BOMBV		;START GIVING SCORES FOR BOMB MODE
	LDA I,40
	STA DELAY		;DELAY ATTRACT MODE SO CENTIPEDE DOES NOT START.
	RTS

59$:	DEY
	BPL 55$			;MAYBE
60$:	RTS

99$:	.BYTE 6,6,0,8		;HALF-SIZE OF OBJECTS
	.BYTE 10.,10.,10.,10.
	.BYTE 0,0,0,0
	.BYTE 0,6,6,0

;ENTRY	(X)=MOTION OBJECT INDEX
;	(Y)=0
;	(A)=PLAYFIELD STAMP
;	(OBST,OBST+1)=PLAYFIELD ADDRESS
;EXIT	(CC)=CARRY SET IF OBJECT DESTROYED
DDTEXP:	CMP I,CLOUD
	BCC 25$			;NO DDT EXPLOSION
	CMP I,DDT
	BCS 25$			;NO DDT EXPLOSION
DDTEX1	=.
	LDA I,80
DDTEX2	=.
	JSR SHOOT2		;PROCESS COLLISION RESULTS
	BCC 20$			;NO EXPLOSION OR POINTS THIS TIME
	STY TEMP1		;MSB OF SCORE
	JSR SCORNG		;SCORES FOR ALL THE CREATURES
	LDA I,14
	STA CHAN2		;EXPLOSION SOUND
	LDA I,0FF
	STA X,MOBJP		;EXPLOSION PICTURE
	LDA I,0F9		;SET COLOR FOR "OVRLAP"
	STA X,MOBJC		;RESTORE COLOR IN CASE IT WAS POISONED ON A BODY SEGMENT
	SEC			;FLAG DEATH OF CREATURE
20$:	RTS

25$:	CLC
	RTS


;
;EXIT	(CC)=NON-ZERO IF WE HIT SOMETHING
SHOOT1:	LDA SHOTH
	STA TEMP1		;WILL BE USED TO CHECK OBSTACLES
	LDA I,2
	BIT CKIND
	BPL 5$			;IF UPRIGHT
	LDA I,0FD
5$:	CLC			;NOW SHOT WILL NOT GO ABOVE THE PLAYFIELD STAMP
	ADC SHOTV
	LDY I,0			;GIVE NO DIRECTION
	JSR OBSTAC		;CHECK FOR OBSTACLES
	BEQ 11$			;NO OBSTACLE ENCOUNTERED
	CMP I,CLOUD
	BCC 108$		;IF A LETTER
	CMP I,DDT
	BCC 11$			;IF DDT EXPLOSION
	BEQ 16$			;IF A DDT BOMB
	CMP I,DDT+2
	BCC 18$			;WE HIT A DDT BOMB
	CMP I,ROCK+1
	BCC 108$		;IF A ROCK (RETURN WITH NON-ZERO FLAG)
	SBC I,1			;CARRY IS SET
	CMP I,7B
	BEQ 106$		;END OF MUSHROOM
	CMP I,77
	BEQ 106$		;END OF MUSHROOM
	CMP I,74
	BEQ 106$		;WE KILLED A GROWING MUSHROOM
	CMP I,70
	BNE 107$		;IF NOT A DYING MUSHROOM
106$:	LDA I,0
	STA TEMP1
	LDA I,01
	JSR SCORNG		;ADD 1 POINT FOR MUSHROOM
	LDY I,0
	TYA			;LEAVE IT ZERO IN THE PLAYFIELD
107$:	STA TEMP1
	LDA NY,OBST
	AND I,80
	ORA TEMP1		;KEEP BACKGROUND COLOR THE SAME
	STA NY,OBST		;NEW PLAYFIELD STAMP
	AND I,7F
	BNE 108$		;IF MUSHROOM DID NOT DISAPPEAR
	JSR MUSHDC		;DEC MUSHROOMS
108$:	LDA I,1			;SET FLAG NON ZERO
	RTS

16$:	BIT CKIND
	BPL 25$			;IF UPRIGHT
	BMI 20$			;IF COCKTAIL

18$:	BIT CKIND
	BMI 25$			;IF COCKTAIL
20$:	LDA OBST
	SEC
	SBC I,20		;PUT PICTURE WINDOW IN CORRECT POSITION
	STA OBST
	BCS 25$			;NO UNDERFLOW
	DEC OBST+1
25$:	JSR DDTS1		;SET UP PARAMETERS
30$:	LDA X,DDTADD
	CMP OBST+1
	BNE 35$			;TRY NEXT ENTRY
	LDA X,DDTADD-1
	CMP OBST
	BEQ 40$			;WE FOUND THE ENTRY
35$:	DEX
	DEX
	DEC TEMP2
	BPL 30$			;DDT MUST BE STARTING TO EXPLODE
11$:	LDA I,0			;RETURN WITH ZERO STATUS
	RTS

40$:	LDA I,0
	TAY
	LDA NY,OBST
	AND I,80		;SAVE COLOR FOR PLAYERS AREA
	STA NY,OBST		;CLEAR EXISTING DDT BOMB
	LDY I,20
	STA NY,OBST
	LDA OBST
	SEC
	SBC I,61
	STA X,DDTADD-1
	LDA OBST+1
	SBC I,0
	ORA I,PLYFLD+400/100+0A0
	STA X,DDTADD		;START AN EXPLOSION
	LDA I,8
	STA TEMP1
	LDA I,0
	JSR SCORNG		;80 POINTS FOR HITTING DDT
	LDX PLAYR
	INC X,HITDDT		;WE HIT A DDT
	LDA I,24
	STA CHAN2		;START SOUND
	RTS			;RETURN WITH NON-ZERO STATUS


;
;ENTRY	(X)=MOTION OBJECT INDEX
;	(A)=NEGATIVE IF KILLED BY DDT CLOUD
;EXIT	(CC)=CARRY SET IF SCORING WILL OCCUR
;	(A)=LSB OF SCORE
;	(Y)=MSB OF SCORE
SHOOT2:	STA TEMP3+1
	LDA X,MOBJP
	AND I,3F
	CMP I,08
	BCC 142$		;IF MILLIPEDE
	CMP I,10
	BCC 70$			;IF A MOSQUITO
	CMP I,14
	BCC 50$			;IF AN INCHWORM
	CMP I,1C
	BCC 13$			;IF SPIDER
	CMP I,1E
	BCC 131$		;IF AN EARWIG
	CMP I,20
	BCC 60$			;IF A DRAGONFLY
	CMP I,38
	BCS 127$		;IF AN BEE
	DEC BEETLS		;DECREASE COUNT OF BEETLES
	DEC SCROLC		;SCROLL PLAYFIELD DOWN
	LDY I,3			;300 POINTS FOR A BEETLE
	LDA MODE
	BMI 65$			;SHOW POINTS FOR THIS IN ATTRACT
	BIT TEMP3+1
	BPL 18$			;IF NO DDT DEATH
	LDY I,9
	BNE 18$			;EXPLODE BEETLE

127$:	LDY I,2			;200 POINTS FOR AN BEE
	BIT TEMP3+1
	BPL 128$		;NO DDT DEATH
	LDY I,6
128$:	LDA I,4
	EOR CKF8
	CMP X,BEEDV
	BEQ 65$			;TIME TO EXPLODE BEE (2ND HIT)
	STA X,BEEDV		;SPEED UP ON FIRST HIT
10$:	CLC			;NO SCORING YET
	RTS

50$:	LDA I,0E0
	STA SLOW		;SLOW DOWN ALL CRITTERS
	LDY I,1			;100 POINTS FOR AN INCHWORM
	LDA MODE
	BMI 65$			;SPECIAL ATTRACT MODE SHOW POINTS FOR INCHWORM
	BIT TEMP3+1
	BPL 18$			;NO DDT DEATH
	LDY I,3
	BNE 18$			;ALWAYS

60$:	LDY I,5			;500 POINTS FOR DRAGONFLY
	BIT TEMP3+1
	BPL 65$			;NO DDT DEATH
	LDY I,15
65$:	JMP SHOOT3		;CHECK FOR EXTRA POINTS IN BOMBING MODE

70$:	INC SCROLC		;SCROLL PLAYFIELD UP
	LDY I,4			;400 POINTS FOR MOSQUITO
	BIT TEMP3+1
	BPL 65$			;NO DDT DEATH
	LDY I,12
	BNE 65$			;ALWAYS

131$:	LDY I,10		;1000 FOR AN EARWIG
	LDA MODE
	BMI 65$			;SPECIAL ATTRACT MODE SHOW EARWIG POINTS
	BIT TEMP3+1
	BPL 132$		;NO DDT DEATH
	LDY I,30
132$:	LDA I,0
	STA CHAN9		;TURN OFF SOUND
18$:	LDA I,0			;LSB OF SCORE
	SEC			;START SCORING
	RTS

13$:	BCC 138$		;ALWAYS

142$:	CPX I,NCENT-1
	BEQ 15$			;END SEGMENT (LAST CENTI)
	LDA X,MOBJC+1
	CMP I,3D
	BNE 15$			;IF NOT A BODY SEGMENT
	LDA I,39
	STA X,MOBJC+1		;SET COLOR TO SHOW THE EYES
15$:	JSR OBSTA0		;GET ADDRESS FOR MUSHROOM
	STX TEMP2		;SAVE X
	JSR MUSHER		;PUT MUSHROOMS ON SCREEN
	LDX PLAYR
	DEC X,DEAD		;REDUCE COUNT OF CENTIPEDES
	LDX TEMP2		;RESTORE X
	LDY I,0
	LDA I,10		;BODY=10 POINTS
	BIT TEMP3+1
	BPL 16$			;NO DDT DEATH
	LDA I,30
16$:	PHA
	LDA X,MOBJC
	CMP I,3D
	PLA
	BCS 145$		;IF BODY SEGMENT
	LSR			;100 POINTS FOR A HEAD
	LSR
	LSR
	LSR
	TAY
	LDA I,0
	BIT MODE		;SPECIAL ATTRACT MODE SHOW 100 POINTS FOR HEAD
	BMI 65$
145$:	SEC			;START SCORING
	RTS

138$:	LDA X,SPDV
	SEC
	SBC PLAYV		;DISTANCE BETWEEN SPIDER AND PLAYER
	JSR ABS
	LDY I,3			;INDEX FOR SCORE
	CMP I,38
	BCS 139$		;IF DISTANCE >=38 THEN 300 POINTS
	DEY
	CMP I,16
	BCS 139$		;IF DISTANCE >=16 THEN 600 INTS
	DEY
	CMP I,0B
	BCS 139$		;IF DISTANCE>=9 THEN 900 POINTS
	DEY			;ELSE 1200
139$:	LDA I,60
	STA X,COUNT2		;START COUNTER
	LDA I,1
	STA CHAN3		;TURN OFF SOUND IF NO OTHER SPIDERS
	BIT TEMP3+1
	BPL 140$		;IF NOT KILLED BY DDT
	LDY I,4			;1800 POINTS
140$:	LDA Y,98$
	STA X,PTS		;SET STAMP CODE
	LDA Y,99$
	TAY
	JMP SHOOT4		;KEEP POINTS COMPLETELY ON THE SCREEN

98$:	.BYTE 32,30,2D,2A,33	;PICTURE CODE FOR SCORE
99$:	.BYTE 12,9,6,3,18	;*100=SCORE

;ENTRY 	(X)=MOTION OBJECT INDEX
;      	(Y)=MSB OF SCORE
;EXIT  	(A)=LSB OF SCORE (0)
;	(CC)=CARRY SET
;	(Y)=MSB OF SCORE
;USES  	A,Y,TEMP1
SHOOT3:	LDA BOMBV
	BEQ 60$			;IF NOT IN BOMB MODE
	LDA MODE
	CMP #0FE		;MODE FE, SORT OF BOMB MODE(NON-SEQUENTIAL SCORING)
	BNE 5$
	TYA
	BNE 20$			;JUST DISPLAY THE SCORE	
5$:	LDA BOMBV
	CMP I,10
	BEQ 15$			;IF AT MAX ALREADY
	STY TEMP1
	CMP TEMP1
	BCS 10$			;IF OLD SCORE IS HIGHER THAN NEW SCORE
	TYA			;USE NEW SCORE
	CMP I,10
	BCC 20$			;IF NOT AT MAX
	LDA I,10		;USE MAX
	BNE 15$			;ALWAYS

10$:	CLC
	SED
	ADC I,01		;ADD 100 POINTS FOR THIS NEXT OBJECT
	CLD
15$:	TAY
20$:	STA BOMBV		;LAST SCORE USED
	CMP I,10
	BCC 30$			;NOT AT MAX
	LDA I,0A		;USE HEX BASE NOT DECIMAL
30$:	CLC
	ADC I,27
	STA X,PTS		;SAVE STAMP FOR SCORES
SHOOT4	=.
	LDA I,0F0
	CMP X,MOBJH
	BCC 40$			;IF OFF LEFT EDGE
	LDA I,10
	CMP X,MOBJH
	BCC 50$			;IF IN THE MIDDLE
40$:	STA X,MOBJH
50$:	LDA I,0			;LSB OF SCORE
60$:	SEC
	RTS


	.SBTTL	SLOWDN-PROCESS SHOW DOWN MODE
;
;EXIT (CC)=CARRY SET IF NO MOTION OBJECT UPDATE
SLOWDN:	DEC SLOW		;DECREASE TIMIR
	LDA SLOW
	BNE 10$			;SOUND STILL GOING
	STA CHAN8		;NO MORE SOUND
	CLC
	RTS

10$:	LDX I,11
	STX CHAN8		;KEEP SOUND GOING
	AND I,3
	ASL
	ASL
	ASL
	ASL			;0,TO,20 OR 30
	STA TEMP1
	LDA SLOW
	AND I,0FC
	LSR			;0 TO 6F
	ADC TEMP1
	STA AUDF0		;SET FREQUENCY
	LDA FRAME
	LSR
	RTS

SECUR9:	.BYTE 0D,9,0C,0C	;"MILLIPEDE BY ED LOGG" IN 6 BIT CDC DISPLAY CODE
	.BYTE 9,10,5,4		;A=1,B=2,...,Z=1A
	.BYTE 5,0,2,19
	.BYTE 0,5,4,0
	.BYTE 0C,0F,7,7
	.PAGE
	.SBTTL	SPDMV-MOVE SPIDER
;SPDMV-UPDATE POSITION OF SPIDER
;
;
SPDMV:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BNE 2$			;DO NOT MOVE SPIDER IF PLAYER IS DEAD
	LDX I,6			;ALLOW UP TO 8 SPIDERS
6$:	LDA X,SPDC
	BEQ 8$			;IF NO PICTURE, CHECK COUNTDOWN TIMER
	LDA X,SPDP
	CMP I,14
	BCC 7$			;IF NOT A SPIDER
	CMP I,1C
	BCS 7$			;THIS IS A SPIDER
	JMP 18$			;MOVE SPIDER

7$:	INX
	CPX I,14.
	BCC 6$			;TRY NEXT ENTRY
2$:	RTS

8$:	LDA MODE
	BMI 7$			;NO SPIDERS-LET ATTRACT DO IT
	STX TEMP1
	CPX I,NCENT
	BCS 100$		;IF NOT A CENTIPEDE ENTRY
	LDX PLAYR
	LDA X,DEAD
	BEQ 11$			;IF ALL CENTIPEDE SEGMENTS ARE DEAD
	LDA X,CENTIN
	CMP I,0C
	BNE 11$			;IF NOT THE FIRST WAVE
	LDA X,SCORE2
	SEC
	SBC I,10
	BCC 11$			;IF BELOW 100,000 NO EXTRA SPIDERS
	LSR
	CMP I,08
	BCC 105$		;IF BELOW 200,000
	LDA I,5
105$:	CLC
	ADC I,3			;NUMBER OF SPIDERS ALLOWED
	STA TEMP1+1
	LDA I,NCENT+1
	SEC
	SBC TEMP1+1		;LAST A CENTIPEDE INDEX USED BY CENTIPEDE
	CMP TEMP1		;CURRENT INDEX
	BCS 11$			;THIS ENTRY IS RESERVED FOR CENTIPEDE
	LDX TEMP1
100$:	DEC X,COUNT2
	BNE 7$			;WAIT FOR THE RIGHT TIME
	TXA
	LDX PLAYR
	CMP I,13.
	BEQ 9$			;THIS ENTRY IS RESERVED FOR SPIDERS
	LDA X,SCORE2
	CMP I,3
	BCC 11$			;WAIT UNTIL SCORE IS OVER 30,000
	LDA I,11.*16.
	SEC
	SBC X,SCORE2		;YES I KNOW THIS IS A DECIMAL NUMBER
	LSR
	LSR
	LSR
	LSR			;10. TO 1
	CMP X,CENTIN
	BCC 9$			;IT'S OK TO HAVE 2 SPIDERS
11$:	LDX TEMP1		;RESTORE X
	BNE 7$

9$:	LDY I,2
	LDA X,SCORE2  
	BNE 10$			;IF SCORE > 9999
	LDA X,SCORE1
	BIT OPTNS1
	BVC 13$			;IF "EASY" THEN SPIDER IS SLOW TILL 10,000
	CMP I,50
	BCS 10$			;IF "HARD" THEN SPIDER IS FASTER AFTER 5000
13$:	LDY I,1				
10$:	LDX TEMP1		;RESTORE X
	BIT CKIND
	BPL 12$			;NOT COCKTAIL
	TYA
	JSR COMP		;REVERSE V DIRECTION
	TAY
12$:	STY X,SPDDV
	LDA RND0
	AND I,04
	BEQ 15$			;VARY SIDE SPIDER COMES OUT ON
	TYA
	JSR COMP
	TAY
15$:	STY X,SPDDH		;SET H DIRECTION
	LDA I,60
	EOR CKF8		;0 OR F8
	STA X,SPDV		;VPOS
	LDA RND0
	AND I,2C
	ORA I,0C
	STA X,COUNT2		;DELAY CHANGE IN DIRECTION
	LDA I,14
	STA X,SPDP		;PICTURE
	LDA I,0B9
	STA X,SPDC		;TURN ON COLOR
18$:	JSR SPDMV1		;GET NEW SPIDER PICTURES
	LDA CHAN3
	BNE 22$			;IF SOUND IS GOING
	LDA I,14
	STA CHAN3		;KEEP SOUND GOING
22$:	DEC X,COUNT2
	BNE 40$			;CONTINUE IN SAME DIRECTION
	BIT RND0
	BMI 30$			;(USE RAND NO. GEN.)DO NOT CHANGE DIRECTION
	LDA X,SPDDH
	BEQ 24$			;IF HDIR=0
	LDY X,SPDH
	CPY I,0FB
	BCS 30$			;IF AT LEFT EDGE DON'T CLEAR HDIRECTION
	CPY I,05
	BCC 30$			;IF AT RIGHT EDGE DON'T CLEAR H DIRECTION
	STA X,OLDDH
	LDA I,0
	BEQ 25$			;ALWAYS

24$:	LDA X,OLDDH
25$:	STA X,SPDDH		;STORE NEW HDIR
30$:	LDA OPTNS1
	AND I,40		;GET DIFFICULTY SWITCH
	LSR
	LSR
	ORA I,20		;EITHER 1/4 OR 1/2 PROBABILITY
	TAY			;20 FOR "EASY" AND 30 FOR "HARD"
	AND RND0
	BEQ 35$			;DO NOT CHANGE DIRECTION
	LDA X,SPDDV
	JSR COMP
	STA X,SPDDV		;REVERSE V DIRECTION
35$:	TYA
	EOR I,10		;20 FOR "HARD" AND 30 FOR "EASY"
	STA X,COUNT2		;DELAY CHANGE INDIRECTION
40$:	LDA X,SPDH
	SEC
	SBC X,SPDDH
	STA X,SPDH
	BNE 45$			;IF NOT OFF THE SCREEN
	LDA I,1
	STA CHAN3		;TURN OFF AUDIO IF NO OTHER SPIDER
	JSR SPDOFF		;REMOVE SPIDER
42$:	JMP 7$			;TRY NEXT SPIDER

45$:	STA TEMP1		;AJUST CALL FOR OBSTAC ROUTINE
	LDA X,SPDV
	SEC
	SBC X,SPDDV
	STA X,SPDV
	LDY I,0
	JSR OBSTAC		;CHECK FOR PLAYFIELD OBSTACLE
	BEQ 60$
	CMP I,CLOUD
	BCC 60$			;DO NOT LET SPIDER EAT MESSAGES
	CMP I,DDT
	BCS 50$			;IF NOT DDT CLOUD
	JSR DDTEX1		;PROCESS DEATH IF SPIDER
	BCS 42$			;ALWAYS

50$:	CMP I,ROCK
	BCC 60$			;DO NOT LET SPIDER EAT DDT
	LDA NY,OBST
	AND I,80		;LEAVE GREY AREA ALONE
	STA NY,OBST		;REMOVE OBSTACLE
	STX TEMP1
	JSR MUSHDC		;DECREMENT MUSHROOMS
	LDX TEMP1		;RESTORE INDEX
60$:	LDA X,SPDV
	EOR CKF8
	CMP I,9
	BCS 70$			;IF NOT AT BOTTOM
	LDA X,SPDDV
	EOR CKF8
	BPL 85$			;CHANGE V DIRECTION
	BMI 80$			;ALWAYS

70$:	STX TEMP1
	LDX PLAYR
	LDA X,SCORE2  
	LDX TEMP1
	SED
	SEC
	SBC I,6
	CLD
	BPL 71$			;IF SCORE >=60K OR >=860K
	LDA I,0			;USE MIN
71$:	LSR			;Y=(SCORE2-6)/2
	CMP I,5
	BCC 72$			;SCORE < 160K
	CMP I,9
	LDA I,5
	BCC 72$			;IF LESS THAN 180K
	LDA I,6			;LIMIT AFTER 180K
72$:	ASL
	ASL
	ASL			;X=Y*8
	STA TEMP2
	LDA I,60
	EOR CKF8
	SEC
	SBC TEMP2		;60-X (OR 98-X FOR COCKTAIL)
74$:	BIT CKIND
	BPL 75$			;NOT COCKTAIL NOW
	CMP X,SPDV
	BCC 80$			;NOT AT TOP
	BCS 76$			;ALWAYS

75$:	CMP X,SPDV
	BCS 80$			;NOT AT TOP
76$:	LDA X,SPDDV
	EOR CKF8		;IN CASE SPIDER STARTED ABOVE TOP LIMIT
	BMI 85$			;CHANGE V DIRECTION
80$:	JSR OVRLAP
	BCC 90$			;NO OVERLAP OF SEGMENTS
85$:	LDA X,SPDDV
	JSR COMP
	STA X,SPDDV		;REVERSE V DIRECTION
90$:	JSR PLAY		;CHECK FOR PLAYER COLLISION
	BCC 95$			;IF PLAYER COLLISION OCCURRED
	JMP 7$

95$:	RTS

SPDOFF:	LDA I,0
	STA X,PTS		;CLEAR FLAG SO "EXPLODE"
	STA X,SPDC		;DOESN'T THINK THIS IS A SPIDER
	STA X,SPDH		;REMOVE SPIDER FROM THE SCREEN
	LDA I,60		;SET DELAY BEFORE ENTERING NEXT SPIDER
	STA X,COUNT2
	RTS

SPDMV1:	LDA X,SPDDV		;-2,-1, 1 OR 2
	JSR ABS			;1 IF SLOW, 2 IF FAST
	AND I,01		;1 IF SLOW, 0 IF FAST
	AND FRAME
	BNE 50$			;NO CHANGE THIS FRAME
	LDA X,SPDDV
	ASL
	LDA I,1
	BCC 10$			;IF MOVING DOWN
	LDA I,-1
10$:	CLC
	ADC X,SPDP
	AND I,03
	ORA I,14		;USE UP/DOWN PICTURES
	STA X,SPDP
50$:	RTS


SECUR5:	.REPT 8-.&07		;ALLIGN ON 8 BYTE BOUNDARY
	.BYTE 0FF
	.ENDR
	.INCLUD MLCPY		;<<<<<<<<<<<<<<<<<<<<<<<<
	.=.-38			;MAKE ROOM BY LEAVING ONLY "C 1982 ATARI"


	.SBTTL	WRMMV-ENTER AND MOVE INCH WORM
WRMMV:	LDA PLAYP
	EOR I,0A
	ORA PEXPLD
	BEQ 5$			;IF PLAYER IS ALIVE
1$:	RTS

5$:	LDA MOBJC
	BNE 20$			;IF ENTRY IS BEING USED
	LDA FRAME
	CMP I,13
	BNE 1$			;NO INCHWORM NOW
	LDA FRAME+1
	AND I,3
	BNE 1$			;EVERY 16 RECORDS
	LDX PLAYR
	LDA X,CENTIN
	CMP I,11.
	BCS 1$			;TOO SOON TO ENTER INCHWORM
	LDA X,DEAD
	BEQ 1$			;DO NOT ENTER IF CENTIPEDE IS DEAD
	JSR WRMMV3		;ENTER AN INCHWORM
20$:	LDA MOBJP
	CMP I,14
	BCS 1$			;NOT AN INCHWORM
	CMP I,10
	BCC 1$			;NOT AN INCHWORM
	ADC I,0			;CARRY WAS SET = ADD 1
	AND I,13
	TAY
	LDA MOBJDH		;-2, -1, 1 OR 2
	JSR ABS			;1 OR 2
	ORA I,01		;1 IF SLOW, 3 IF FAST
	EOR I,02		;3 IF SLOW, 1 IF FAST
	AND FRAME
	BNE 25$			;UPDATE PICTURE LESS FREQUENTLY
	STY MOBJP		;UPDATE PICTURE
25$:	LDA CHAN8
	BNE 28$			;IF SOUND IS ON
	LDA I,10
	STA CHAN8		;REPEAT SOUND
28$:	LDA MOBJH
	CLC
	ADC MOBJDH
	STA MOBJH
	BNE 30$			;IF NOT OFF THE SCREEN
	STA MOBJC		;REMOVE FROM THE ACTIVE LIST
30$:	STA TEMP1
	LDA MOBJV
	LDY I,0
	JSR OBSTAC		;GET PLAYFIELD ADDRESS
	LDX I,0
	JMP DDTEXP		;PROCESS EXPLOSIONS IF ANY


WRMMV3:	LDA I,10
	STA MOBJP		;SET PICTURE
	LDA I,0B9
	STA MOBJC		;SET COLOR
	LDA I,0
	STA MOBJH		;SET HORIZONTAL
	LDA RND1
	AND I,38
	CLC
	ADC I,40
	STA MOBJV		;SET VERTICAL
	LDY PLAYR
	LDA I,2
	LDY X,SCORE2
	CPY I,8
	BCS 10$			;IF MORE THAN 80000
	LDA I,1
10$:	BIT RND0
	BPL 20$			;RANDOMLY CHOOSE A DIRECTION
	JSR COMP		;GO OPPOSITE DIRECTION
20$:	STA MOBJDH
	RTS

CKSUM1:	.BYTE 038		;SECOND 4K CHECKSUM

	.END
                                                                                                                                                                                                   